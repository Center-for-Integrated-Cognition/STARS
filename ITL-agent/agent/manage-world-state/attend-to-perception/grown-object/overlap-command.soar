# For each belief object that intersects the grown-object 
#   that is not visible (no perception-obj) 
# find out how much it is overlapped by the grown-object

# overlap-command: see agent/svs-command-operators
sp {attend-to-perception*grown-object*propose*overlap-command*for*intersecting*objects
   (state <s> ^name attend-to-perception
              ^discrepancy-type grown-object
              ^object-info.perception-obj.id <perc-id>
              ^intersecting-non-visible-object <belief-id>
             -^overlap-results.result.object-a <belief-id>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name overlap-command
        ^object-a <belief-id>
        ^object-b <perc-id>)
}

sp {attend-to-perception*grown-object*apply*overlap-command*copy*result
   (state <s> ^name attend-to-perception
              ^discrepancy-type grown-object
              ^operator.name overlap-command
              ^overlap-command <cmd>
              ^overlap-results <results>)
   (<cmd> ^result <res>)
   (<res> ^overlap <> none)
-->
   (<s> ^overlap-command <cmd> -)
   (<results> ^result <res>)
}

sp {attend-to-perception*grown-object*apply*overlap-command*print-perception
   (state <s> ^name attend-to-perception
              ^discrepancy-type grown-object
              ^agent-params.print-perception true
              ^operator.name overlap-command
              ^overlap-command.result <res>)
   (<res> ^object-a <a> ^object-b <b> ^overlap <val>)
-->
   (write |Overlap: | <val> | of | <a> | is contained by | <b> (crlf))
} 
