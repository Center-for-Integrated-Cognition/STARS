### construct status-predicate

sp {task-utils*construct-task-argument*expand*status-predicate*current-location
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type predicate
          ^source <src>)
   (<src> ^type status
          ^name current-location
          ^1 <obj1>)
-->
   (<arg> ^pred-type status)

   (<s> ^construct-task-argument <obj1-arg>)
   (<obj1-arg> ^arg-type object
               ^source <obj1>)
   (<arg> ^create-obj1 <obj1-arg>)
}
          

sp {task-utils*construct-task-argument*propose*construct-task-argument*status-predicate
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^pred-type status
          ^source.name <name>
          ^create-obj1.created-arg.id <obj1>
         -^created-arg)
-->
   (<s> ^operator <o> + =)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type predicate
        ^pred-type status
        ^status-name <name>
        ^1 <obj1>)
}

sp {task-utils*construct-task-argument*apply*construct-task-argument*status-predicate
   (state <s> ^operator <o>)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type predicate
        ^pred-type status
        ^status-name <name>
        ^1 <obj1>)
-->
   (<arg> ^created-arg <carg>)
   (<carg> ^type status
           ^name <name>
           ^1 <obj1>)
}

