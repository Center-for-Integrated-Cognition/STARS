### GENERALIZE CONCEPT

# procedural match - the concept matches one on the reference-task-op
sp {task-utils*generalize-task-argument*propose*generalize-task-argument*concept*procedural
  (state <s> ^generalize-task-argument <arg>
             ^reference-tcn-proc.<arg-name> <parg>
             ^reference-task-op.<arg-name>.handle <handle>)
  (<arg> ^arg-type concept
         ^instantiation.handle <handle>
        -^general-arg-rep)
  (<parg> ^arg-type concept
          ^id <conc-slot>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type procedural
       ^id <conc-slot>)
}

sp {task-utils*generalize-task-argument*apply*generalize-task-argument*concept*procedural
  (state <s> ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type procedural
       ^id <conc-slot>)
-->
  (<arg> ^general-arg-rep <gen>)
  (<gen> ^id <conc-slot>)
}

# default match - copy the predicates onto a default rep
sp {task-utils*generalize-task-argument*propose*generalize-task-argument*concept*default
  (state <s> ^generalize-task-argument <arg>)
  (<arg> ^arg-type concept
         ^instantiation.handle <handle>
        -^general-arg-rep)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type default
       ^predicate-handle <handle>)
}

sp {task-utils*generalize-task-argument*apply*generalize-task-argument*concept*default
  (state <s> ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type default
       ^predicate-handle <handle>)
-->
  (<arg> ^general-arg-rep <rep>)
  (<rep> ^id <conc-slot>)
  (<conc-slot> ^default <def>)
  (<def> ^predicate-handle <handle>)
}
