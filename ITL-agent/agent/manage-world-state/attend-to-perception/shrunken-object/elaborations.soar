sp {attend-to-perception*shrunken-object*elaborate*object-info
   (state <s> ^name attend-to-perception
              ^discrepancy-type shrunken-object
              ^discrepancy-info.object-info <obj-info>)
-->
   (<s> ^object-info <obj-info>)
}

sp {attend-to-perception*shrunken-object*elaborate*agent-params*contained-threshold
   (state <s> ^name attend-to-perception
              ^discrepancy-type shrunken-object
              ^agent-params.contained-threshold <t>)
-->
   (<s> ^contained-threshold <t>)
}

### We are looking for new perception objects that are contained
#     inside the belief object volume for the shrunken object

sp {attend-to-perception*shrunken-object*elaborate*intersecting-non-visible-object
   (state <s> ^name attend-to-perception
              ^discrepancy-type shrunken-object
              ^intersect-command.result {<perc-id> <> none}
             -^top-state.perception-monitor.object-monitor.object-info.perception-obj.id <perc-id>)
-->
   (<s> ^intersecting-new-object <perc-id>)
}

sp {attend-to-perception*shrunken-object*elaborate*overlap-results
   (state <s> ^name attend-to-perception
              ^discrepancy-type shrunken-object)
-->
   (<s> ^overlap-results <res>)
}

sp {attend-to-perception*shrunken-object*elaborate*contained-fragment-id
   (state <s> ^name attend-to-perception
              ^discrepancy-type shrunken-object
              ^overlap-results.result <res>
              ^contained-threshold <t>)
   (<res> ^object-a <id>
          ^overlap > <t>)
-->
   (<s> ^contained-fragment-id <id>)
}

sp {attend-to-perception*shrunken-object*elaborate*found-contained-fragment
   (state <s> ^name attend-to-perception
              ^discrepancy-type shrunken-object
              ^contained-fragment-id <id>)
-->
   (<s> ^found-contained-fragment true)
}

### object-occluded true/false
sp {attend-to-perception*shrunken-object*elaborate*object-occluded*true
   (state <s> ^name attend-to-perception
              ^discrepancy-type shrunken-object
              ^agent-params.occlusion-threshold <threshold>
              ^occlusion-command-result { >= <threshold> <> failure })
-->
   (<s> ^object-occluded true)
}

sp {attend-to-perception*shrunken-object*elaborate*object-occluded*false
   (state <s> ^name attend-to-perception
              ^discrepancy-type shrunken-object
              ^agent-params.occlusion-threshold <threshold>
              ^occlusion-command-result { < <threshold> <> failure })
-->
   (<s> ^object-occluded false)
}
