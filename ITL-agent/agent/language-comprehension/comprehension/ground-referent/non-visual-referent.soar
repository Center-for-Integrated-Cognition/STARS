sp {ground-referent*propose*non-visual-referent
   (state <s> ^name ground-referent
              ^constraint-lt <cl>
              ^superstate.operator.constraints <cs>)
  -(<cs> ^constraint.property.type visual)
  -{(<cs> ^constraint.relation <rel>)
    (<rel> ^2 <item>)
    (<item> ^item-type object)}
  -(<cl> ^specifier << personal it >>)
  -(<cl> ^demonstrative true)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name non-visual-referent
         ^constraints <cs>)
}

sp {ground-referent*apply*non-visual-referent*referent
   (state <s> ^operator <op>)
   (<op> ^name non-visual-referent)
-->
   (<s> ^referent <r>)
}


sp {ground-referent*apply*non-visual-referent*copy-constraint
   (state <s> ^operator <op>
              ^referent <r>)
   (<op> ^name non-visual-referent
         ^constraints.constraint <c>)
   (<c> ^handle <value> 
          ^property.<< name handle >> <prop>)
-->
   (<r> ^<prop> <value>)
}

sp {ground-referent*apply*non-visual-referent
   (state <s> ^operator <op>
              ^referent <r>)
   (<op> ^name non-visual-referent
         ^constraints <cs>)
   (<r> ^<prop>)
-->
   (<cs> ^referent <r>
         ^ground-tested <cs>)
}
