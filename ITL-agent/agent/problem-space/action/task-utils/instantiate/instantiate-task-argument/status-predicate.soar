### INSTANTIATE STATUS PREDICATE

sp {task-utils*instantiate-task-argument*expand*status*predicate*current-location
  (state <s> ^instantiate-task-argument <arg>)
  (<arg> ^arg-type predicate
         ^general-arg-rep <rep>)
  (<rep> ^type status
         ^name current-location
         ^1 <obj1-slot>)
-->
  (<s> ^instantiate-task-argument <ins-obj1>)

  (<ins-obj1> ^arg-type object
              ^general-arg-rep <gen-obj1>)
  (<gen-obj1> ^id <obj1-slot>)

  (<arg> ^instantiate-obj1 <ins-obj1>)
}

sp {task-utils*instantiate-task-argument*expand*status*predicate*current-waypoint
  (state <s> ^instantiate-task-argument <arg>)
  (<arg> ^arg-type predicate
         ^general-arg-rep <rep>)
  (<rep> ^type status
         ^name current-waypoint
         ^1 <wp1-slot>)
-->
  (<s> ^instantiate-task-argument <ins-wp1>)

  (<ins-wp1> ^arg-type waypoint
             ^general-arg-rep <gen-wp1>)
  (<gen-wp1> ^id <wp1-slot>)

  (<arg> ^instantiate-obj1 <ins-wp1>)
}

sp {task-utils*instantiate-task-argument*propose*instantiate-task-argument*predicate*status
  (state <s> ^instantiate-task-argument <arg>)
  (<arg> ^arg-type predicate
         ^general-arg-rep <rep>
         ^instantiate-obj1.instantiation.id <obj1>
        -^instantiation)
   (<rep> ^type status
          ^name <status-name>)

-->
  (<s> ^operator <o> + =)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type status
       ^status-name <status-name>
       ^1 <obj1>)
}

sp {task-utils*instantiate-task-argument*apply*instantiate-task-argument*predicate*status
  (state <s> ^operator <o>)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type status
       ^status-name <status-name>
       ^1 <obj1>)
-->
  (<arg> ^instantiation <ins>)
  (<ins> ^type status
         ^name <status-name>
         ^1 <obj1>)
}


