# Lookup word - try to find the word for the given label
sp {describe-object*propose*lookup-label
   (state <s> ^name describe-object
             -^ignore-label <label>
              ^desc-object.property <prop>)
   (<prop> ^label <label>
          -^word <word>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-label
        ^category smem
        ^label <label>)
}

sp {describe-object*apply*lookup-smem*label
   (state <s> ^name describe-object
              ^operator.name lookup-label
              ^lookup-label-result <res>
              ^desc-object.property <prop>)
   (<res> ^label <label>
          ^word.word <word>
          ^property.name <prop-name>)
   (<prop> ^name <prop-name>
           ^value <val>)
   (<val> ^label <label>)
-->
   (<val> ^word <word>)
   (<s> ^lookup-label-result <res> -)
}

sp {describe-object*apply*lookup-label*failure
   (state <s> ^name describe-object
              ^operator <o>
              ^lookup-label-result failure)
   (<o> ^name lookup-label
        ^label <label>)
-->
   (<s> ^ignore-label <label>)
}