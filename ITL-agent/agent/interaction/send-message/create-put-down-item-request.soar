sp {send-message*put-down-item-request*elaborate*object-description*item
   (state <s> ^name send-message
              ^message.type put-down-item-request
              ^message.item <obj>)
-->
   (<s> ^object-description <d>)
   (<d> ^object <obj>)
}

sp {send-message*put-down-item-request*elaborate*object-description*location
   (state <s> ^name send-message
              ^message.type put-down-item-request
              ^message.location <loc>)
-->
   (<s> ^object-description <d>)
   (<d> ^object <loc>)
}

sp {send-message*propose*create-put-down-item-request
   (state <s> ^name send-message
              ^message.type put-down-item-request
             -^created-message)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-put-down-item-request)
}

sp {send-message*apply*create-put-down-item-request*no*loc
   (state <s> ^name send-message
              ^operator.name create-put-down-item-request
              ^message <m>
              ^object-description <d1>)
   (<m> ^item <obj>
       -^location)
   (<d1> ^object <obj>
         ^description <desc1>)
-->
   (<s> ^created-message <msg>)
   (<msg> ^type put-down-item-request
          ^fields <f>)
   (<f> ^item <desc1>)
}

sp {send-message*apply*create-put-down-item-request*loc
   (state <s> ^name send-message
              ^operator.name create-put-down-item-request
              ^message <m>
              ^object-description <d1> <d2>)
   (<m> ^item <obj>
        ^location <loc>)
   (<d1> ^object <obj>
         ^description <desc1>)
   (<d2> ^object <loc>
         ^description <desc2>)
-->
   (<s> ^created-message <msg>)
   (<msg> ^type put-down-item-request
          ^fields <f>)
   (<f> ^item <desc1>
        ^location <desc2>)
}
