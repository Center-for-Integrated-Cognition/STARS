###################################################################
#
# interpret-what-do-you-see
# Occurs when the instructor wants to know what Rosie can see in the world
# E.g. "What do you see?"."I can see a red block, blue block and bob in the kitchen"
###################################################################

#######
# OPERATOR PROPOSAL
#######
sp {interpret-message*propose*interpret-what-do-you-see
   (state <s> ^name interpret-message
              ^instructor-message <msg>)
   (<msg> ^message-type what-do-you-sense-question
         -^interpreted true)
-->
#(interrupt)
   (<s> ^operator <o> +)
   (<o> ^name interpret-what-do-you-see
        ^message <msg>)
}

######
# OPERATOR ELABORATIONS
######

# Elaborate useful information from the top-state onto the operator
sp {interpret-message*elaborate*interpret-what-do-you-see*elaborate-world-onto-state
   (state <s> ^name interpret-message
              ^top-state.world <world>
              ^operator <o> +)
   (<o> ^name interpret-what-do-you-see
        ^message <msg>)
   (<msg> ^arg2.current-location <cur-loc>)
   (<world> ^predicates <preds>)
-->
   (<o> ^predicates <preds>)
}


######
# OPERATOR APPLICATION
######
sp {interpret-message*elaborate*interpret-what-do-you-see*create*message
   (state <s> ^name interpret-message
              ^instructor-message <msg>
              ^operator <o>)
   (<o> ^name interpret-what-do-you-see
        ^message <msg>
        ^predicates <preds>)
-->
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type agent-perception-description
        ^parameters <params>
        ^satisfaction <sat>)
   (<params> ^predicates <preds>)
   (<sat> ^dialog-event <de>)
   (<de> ^type explanation
         ^originator agent)
}
  
