#copy relationships that are new (in final state and not initial)
#ignore relationships that are in ignore list                                
sp {state-union*propose*copy-relationships
   (state <s> ^name state-union
             -^copied-relationships)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-relationships)
}

sp {state-union*apply*copy-relationships*mark*copied
   (state <s> ^name state-union
              ^operator.name copy-relationships)
-->
   (<s> ^copied-relationships true)
}

sp {state-union*apply*copy-relationships
   (state <s> ^name state-union
              ^relationships <r>
             -^ignore.relation <name>
              ^operator.name copy-relationships
              ^initial.relationships.relations <rels>
              ^final.relationships.relations <rel2>)
   (<rel2> ^name <name> ^instance <instance>)
   (<instance> ^1 <ida> ^2 <idb> -^3)
   (<rels> ^name <name> ^instance <instance2>)
   (<instance2> ^1 <ida> ^2 <idb> -^3)
 -{(<s> ^ignore.object.id <id>)
     (<instance> ^<< 1 2 3 >> <id>)}
-->
   (<r> ^relations <r1>)
   (<r1> ^name <name> ^instance <instance>)
}
sp {state-union*apply*copy-relationships*3-args
   (state <s> ^name state-union
              ^relationships <r>
             -^ignore.relation <name>
              ^operator.name copy-relationships
              ^initial.relationships.relations <rels>
              ^final.relationships.relations <rel2>)
   (<rel2> ^name <name> ^instance <instance>)
   (<instance> ^1 <ida> ^2 <idb> ^3 <idc>)
   (<rels> ^name <name> ^instance <instance2>)
   (<instance2> ^1 <ida> ^2 <idb> ^3 <idc>)
 -{(<s> ^ignore.object.id <id>)
   (<instance> ^<< 1 2 3 >> <id>)}
-->
   (<r> ^relations <r1>)
   (<r1> ^name <name> ^instance <instance>)
}

#copy objects that appear in the new relationships
#ignore objects from ignore list
sp {state-union*propose*copy-objects
   (state <s> ^name state-union
             -^copied-objects)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-objects)
}

sp {state-union*apply*copy-objects*mark*copied
   (state <s> ^name state-union
              ^operator.name copy-objects)
-->
   (<s> ^copied-objects true)
}

sp {state-union*apply*copy-objects
   (state <s> ^name state-union
              ^relationships.relations <p>
              ^object-set <os>
             -^ignore.object <obj>
              ^object-desc-set <ods>
              ^initial.objects.object <obj>
              ^final.object-desc-set.obj-desc <desc>
              ^final.objects.object <obj>
              ^operator.name copy-objects
              ^relationships <rels>)
   (<p> ^name <name> ^instance <instance>)
   (<instance> ^<< 1 2 3 >> <id>)
   (<obj> ^id <id>)
   (<desc> ^id <id>)
-->
   (<os> ^object <obj>)
   (<ods> ^obj-desc <desc>)
}

# predicates
sp {state-union*propose*copy-predicates
   (state <s> ^name state-union
             -^copied-predicates)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-predicates)
}

sp {state-union*apply*copy-predicates*mark*copied
   (state <s> ^name state-union
              ^operator.name copy-predicates)
-->
   (<s> ^copied-predicates true)
}

sp {state-union*apply*copy-predicates*copy
   (state <s> ^name state-union
              ^operator.name copy-predicates
              ^initial.predicate-set.predicate <initial-pred>
              ^final.predicate-set.predicate <final-pred>
              ^predicate-set <preds>)
   (<initial-pred> ^object <obj> ^name <pred-name> ^value <pred-value>)
   (<final-pred> ^object <obj> ^name <pred-name> ^value <pred-value>)
-->
   (<preds> ^predicate <new-pred>)
   (<new-pred> ^object <obj> ^name <pred-name> ^value <pred-value>)
}

#complete, copy to output state-result
sp {state-union*propose*complete
   (state <s> ^name state-union)
-->
   (<s> ^operator <o> + = <)
   (<o> ^name complete)
}

sp {state-union*apply*complete
   (state <s> ^name state-union
              ^operator.name complete
              ^object-set <os>
              ^relationships <rels>
              ^object-desc-set <ods>
              ^predicate-set <preds>
              ^superstate <ss>)
-->
   (<ss> ^state-result <res>)
   (<res> ^relationships <rels> ^objects <os> ^object-desc-set <ods>
          ^predicate-set <preds>)
}