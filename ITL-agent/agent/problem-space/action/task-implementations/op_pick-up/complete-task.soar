
# When we pick up an object, we remove all predicates related to that object
sp {op_pick-up*apply*complete-task*remove*in*predicate
   (state <s> ^name op_pick-up1
              ^operator <o>
              ^target-object <obj>
              ^world.predicates.predicate <pred>)
   (<o> ^name complete-task
        ^status success)
   (<pred> ^instance <i>)
   (<i> ^<pnum> <obj>)
-->
   (<pred> ^instance <i> -)
}

# For the tabletop, we set the belief bounds to match the arm
sp {op_pick-up*apply*complete-task*success*relocate*belief*volume
   (state <s> ^name op_pick-up1
              ^agent-params.domain tabletop
              ^operator <o>
              ^target-object.svs-obj.id <id>
              ^perception-monitor.input-link.self.pose <pose>
              ^top-state.svs.command <cmd>)
   (<o> ^name complete-task
        ^status success)
   (<pose> ^x <x> ^y <y> ^z <z>)
-->
   (<cmd> ^set_transform <xform>)
   (<xform> ^id <id>
            ^position <pos>)
   (<pos> ^x <x> ^y <y> ^z <z>)
}
