### send the low-level look command to the output-link ###

sp {op_look1*propose*send-look-command
   (state <s> ^name op_look1)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-look-command
        ^item-type command-operator
        ^command-name look-command)
}


### ai2thor ###

sp {op_look1*external*apply*send-look-command*ai2thor
   (state <s> ^name op_look1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain ai2thor
              ^current-task-segment <seg>
              ^direction <dir>
              ^top-state.io.output-link <out>)
   (<o> ^name send-look-command
        ^command-name <cmd-name>)
-->
   (<out> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name look
         ^direction <dir>)
   (<seg> ^<cmd-name> <pa>)
}


### cozmo ###

sp {op_look1*external*apply*send-look-command*cozmo*up
   (state <s> ^name op_look1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain cozmo
              ^current-task-segment <seg>
              ^direction up1
              ^top-state.io <io>)
   (<o> ^name send-look-command
        ^command-name <cmd-name>)
   (<io> ^input-link.self.head-angle <angle>
         ^output-link <out>)
-->
   (<out> ^move-head <mh>)
   (<mh> ^item-type output-command
         ^angle (+ <angle> 0.4) )
   (<seg> ^<cmd-name> <mh>)
}

sp {op_look1*external*apply*send-look-command*cozmo*down
   (state <s> ^name op_look1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain cozmo
              ^current-task-segment <seg>
              ^direction down1
              ^top-state.io <io>)
   (<o> ^name send-look-command
        ^command-name <cmd-name>)
   (<io> ^input-link.self.head-angle <angle>
         ^output-link <out>)
-->
   (<out> ^move-head <mh>)
   (<mh> ^item-type output-command
         ^angle (- <angle> 0.4))
   (<seg> ^<cmd-name> <mh>)
}


