sp {equate-objects*elaborate*objects*superoperator
  (state <s> ^name equate-objects
             ^superstate.operator <o>)
  (<o> ^object1 <obj1>
       ^object2 <obj2>)
-->
  (<s> ^object1 <obj1>
       ^object2 <obj2>)
}


sp {equate-objects*elaborate*category*learn-storage-info
  (state <s> ^name equate-objects
             ^object1 <obj1>
             ^object2 <obj2>)
  (<obj1> ^predicates.{ <att> << spatial-shape modifier1 >> } <val>
          ^relations <rel>)
  (<rel> ^handle of1
         ^2 <rel-obj>)
-->
  (<s> ^category learn-storage-info
       ^link-head-object <rel-obj>
       ^link-name <val>
       ^link-tail-object <obj2>)
}

sp {equate-objects*elaborate*link-head-smem
  (state <s> ^name equate-objects
             ^category learn-storage-info
             ^link-head-object <obj>)
  (<obj> ^object-instance true)
-->
  (<s> ^link-head-smem <obj>)
}

sp {equate-objects*elaborate*link-head-smem*object-info
  (state <s> ^name equate-objects
             ^category learn-storage-info
             ^link-head-object <obj>)
  (<obj> -^object-instance
          -^in-smem
          ^predicates.{ <att> << spatial-shape shape >> } <val>)
-->
  (<s> ^link-head-smem <new>)
  (<new> ^item-type object-info ^shape <val>)
}

sp {equate-objects*learn-storage-info*smem-query*head*smem
  (state <s> ^name equate-objects
             ^category learn-storage-info
             ^link-head-object <obj>
             -^link-head-smem)
  (<obj> ^in-smem true
         ^handle <h>)
-->
  (<s> ^smem-query.cue.handle <h>)
}

sp {equate-objects*learn-storage-info*copy*smem-query*result*object2*link-head-smem
  :o-support
  (state <s> ^name equate-objects
             ^category learn-storage-info
             ^link-head-object.handle <h>
             ^smem-query.result <res>)
  (<res> ^handle <h>)
-->
  (<s> ^link-head-smem <res>)
}

sp {equate-objects*elaborate*link-tail-smem
  (state <s> ^name equate-objects
             ^category learn-storage-info
             ^link-tail-object <obj>)
  (<obj> ^object-instance true)
-->
  (<s> ^link-tail-smem <obj>)
}

sp {equate-objects*learn-storage-info*smem-query*object2*smem
  (state <s> ^name equate-objects
             ^category learn-storage-info
             ^link-tail-object <obj>
             -^link-tail-smem)
  (<obj> ^in-smem true
         ^handle <h>)
-->
  (<s> ^smem-query.cue.handle <h>)
}

sp {equate-objects*learn-storage-info*copy*smem-query*result*object2*link-tail-smem
  :o-support
  (state <s> ^name equate-objects
             ^category learn-storage-info
             ^link-tail-object.handle <h>
             ^smem-query.result <res>)
  (<res> ^handle <h>)
-->
  (<s> ^link-tail-smem <res>)
}

sp {equate-objects*learn-storage-info*elaborate*link*and*store
  (state <s> ^name equate-objects
             ^category learn-storage-info
             ^link-head-smem <head>
             ^link-name <name>
             ^link-tail-smem <tail>
             ^smem.command <cmd>)
-->
  (<head> ^<name> <tail>)
  (<cmd> ^store <head>)
}


