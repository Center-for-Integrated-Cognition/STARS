##### change-perception-id
# Change the perception-id of the given object

sp {anystate*apply*change-world-state*change-perception-id*add*new
   (state <s> ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name change-world-state
        ^change-perception-id <chg>)
   (<chg> ^object-info <obj-info>
          ^perception-id <perc-id>)
-->
   (<obj-info> ^perception-id <perc-id>)
}

sp {anystate*apply*change-world-state*change-perception-id*remove*old
   (state <s> ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name change-world-state
        ^change-perception-id <chg>)
   (<chg> ^object-info <obj-info>
          ^perception-id <perc-id>)
   (<obj-info> ^perception-id { <old-id> <> <perc-id> })
-->
   (<obj-info> ^perception-id <old-id> -)
}

# If print-perception is on, print a message
sp {anystate*apply*change-world-state*change-perception-id*print-perception
   (state <s> ^operator <o>
              ^agent-params.print-perception true)
   (<o> ^name change-world-state
        ^change-perception-id <chg>)
   (<chg> ^object-info.object-handle <obj-handle>
          ^perception-id <perc-id>)
-->
   (write |change-world-state: change-perception-id on object | <obj-handle> | to | <perc-id> (crlf))
}
   
