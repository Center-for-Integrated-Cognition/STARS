# If we have the target-object but don't have its coordinate, query svs for its position

sp {op_face*propose*extract-target-position
   (state <s> ^name op_face1
              ^problem-space.execution-type external
              ^target-object.svs-obj.id <perc-id>
             -^target-coord)
-->
   (<s> ^operator <o> + =)
   (<o> ^name extract-target-position
        ^target-id <perc-id>)
}

sp {op_face*apply*extract-target-position*create*target*position*filter
   (state <s> ^name op_face1
              ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name extract-target-position
        ^target-id <tar-id>)
-->
    (<cmd> ^extract_once <tar-pos>)
    (<tar-pos> ^type node_position
               ^a <tar>)
    (<tar> ^type node
           ^id <tar-id>)
    (<s> ^target-position-filter <tar-pos>)
}

sp {op_face*apply*extract-target-position*copy*result
   (state <s> ^name op_face1
              ^operator.name extract-target-position
              ^target-position-filter <filter>
              ^top-state.svs.command <cmd>)
   (<cmd> ^extract_once <filter>)
   (<filter> ^result.record.value <tar-pos>)
   (<tar-pos> ^x <tx> ^y <ty>)
-->
   (<s> ^target-coord <coord>)
   (<coord> ^x <tx> ^y <ty>)

   (<s> ^target-position-filter <filter> -)
   (<cmd> ^extract_once <filter> -)
}

