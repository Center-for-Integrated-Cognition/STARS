sp {evaluate-operator*elaborate*state
   (state <s> ^superstate.operator <o>)
   (<o> ^name evaluate-operator
        ^evaluation.current-evaluation-depth <ced>)
-->
   (<s> ^current-evaluation-depth <ced>)
}

sp {evaluate-operator*elaborate*symbolic-evaluation*from-subgoal*depth-exceeded
   :default
   (state <ss> ^superstate <s>
               ^quiescence t
               ^depth-exceeded <eb>)
   (<s> ^operator <so>
        ^evaluation <e>)
   (<so> ^name evaluate-operator
         ^evaluation <e>
         ^evaluation.desired <eb>)
-->
   (<e> ^symbolic-value depth-exceeded)}

sp {evaluate-operator*elaborate*symbolic-evaluation*from-subgoal*depth-exceeded*duplicate-desired
   :default
   (state <ss> ^superstate <s>
              ^quiescence t
               ^depth-exceeded <eb>)
   (<eb> ^duplicate-of <des>)
   (<s> ^operator <so>
        ^evaluation <e>)
   (<so> ^name evaluate-operator
         ^evaluation <e>
         ^evaluation.desired <des>)
-->
   (<e> ^symbolic-value depth-exceeded)}

sp {evaluate-operator*elaborate*symbolic-evaluation*from-subgoal
   :default
   (state <ss> ^superstate <s>
               ^ { << required-success success partial-success
                   indifferent partial-failure failure prohibit-failure
                   >> <svalue> } <eb>)
   (<s> ^operator <so>
        ^evaluation <e>)
   (<so> ^name evaluate-operator
         ^evaluation <e>
         ^evaluation.desired <eb>)
-->
   (<e> ^symbolic-value <svalue>)}
