# Don't attend to missing-object on the object being grabbed
sp {primitive-pick-up*tabletop*reject*attend-to-missing-object
   (state <s> ^name primitive-pick-up
              ^agent-params.domain tabletop
              ^operator <o> +
              ^target-object <obj>)
   (<o> ^name attend-to-perception
        ^change.type missing-object
        ^change.object-info.world-obj <obj>)
-->
   (<s> ^operator <o> -)
}

## Don't attend to new objects while the arm is moving
sp {primitive-pick-up*tabletop*reject*attend-to-new-perception-object
   (state <s> ^name primitive-pick-up
              ^agent-params.domain tabletop
              ^operator <o> +
              ^executing-command true)
   (<o> ^name attend-to-perception
        ^change.type new-perception-object)
-->
   (<s> ^operator <o> -)
}

# Don't attend at all to the object being picked up while the arm is moving
sp {primitive-pick-up*tabletop*reject*attend-to-perception*grabbed1*object
   (state <s> ^name primitive-pick-up
              ^agent-params.domain tabletop
              ^operator <o> +
              ^target-object <obj>
              ^executing-command true)
   (<o> ^name attend-to-perception
        ^change.object-info.world-obj <obj>)
-->
   (<s> ^operator <o> -)
}

sp {primitive-pick-up*tabletop*elaborate*evaluate-new-object*candidate-for-missing-object
   (state <s> ^name primitive-pick-up
              ^agent-params.domain tabletop
              ^operator <o> +
              ^target-object.handle <obj-handle>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<o> ^name attend-to-perception
        ^change.type new-object
        ^change <change>)
   (<obj-info> ^object-handle <obj-handle>)
-->
   (<change> ^candidate-for-missing-object <obj-info>)
}

