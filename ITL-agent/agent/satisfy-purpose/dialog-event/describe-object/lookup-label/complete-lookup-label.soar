# Finish the lookup
sp {lookup-label*propose*complete-lookup-label
   (state <s> ^name lookup-label
              ^sense <sense>)
-->
   (<s> ^operator <o> + <)
   (<o> ^name complete-lookup-label)
}

sp {lookup-label*apply*complete-lookup-label
   (state <s> ^name lookup-label
              ^operator.name complete-lookup-label
              ^superstate <ss>
              ^sense <sense>)
-->
   (<ss> ^lookup-label-result <sense>)
}

# Updates any objects with the word, for convenience
sp {lookup-label*apply*complete-lookup-label*annotate*object
   (state <s> ^name lookup-label
              ^operator.name complete-lookup-label
              ^sense <sense>
              ^top-state.world.objects.object.property <prop>)
   (<sense> ^label <label>
            ^property.handle <prop-handle>
            ^word.word <word>)
   (<prop> ^handle <prop-handle>
           ^value <val>)
   (<val> ^label <label>
         -^word <word>)
-->
   (<val> ^word <word>)
}

