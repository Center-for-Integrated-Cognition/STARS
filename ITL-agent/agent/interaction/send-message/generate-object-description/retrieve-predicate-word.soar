sp {generate-object-description*propose*retrieve-predicate-word
   (state <s> ^name generate-object-description
              ^lookup-predicate <lp>)
   (<lp> ^predicate <pred>
        -^word-lti)
-->
   (<s> ^operator <o> + =)
   (<o> ^name retrieve-predicate-word
        ^lookup-predicate <lp>)
}

sp {generate-object-description*apply*retrieve-predicate-word*create*query
   (state <s> ^name generate-object-description
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name retrieve-predicate-word
        ^lookup-predicate <lp>)
   (<lp> ^predicate <pred>)
-->
   (<cmd> ^query <q>)
   (<q> ^name <pred>)
   (<lp> ^predicate-query <q>)
}

sp {generate-object-description*apply*retrieve-predicate-word*retrieve*predicate*success
   (state <s> ^name generate-object-description
              ^operator <o>
              ^smem.result.success <q>
              ^smem.result.retrieved <lti>
              ^smem.command <cmd>)
   (<o> ^name retrieve-predicate-word
        ^lookup-predicate <lp>)
   (<lp> ^predicate-query <q>)
   (<cmd> ^query <q>)
-->
   (<cmd> ^query <q> -)
   (<lp> ^predicate-lti <lti>)
}

sp {generate-object-description*apply*retrieve-predicate-word*retrieve*predicate*failure
   (state <s> ^name generate-object-description
              ^operator <o>
              ^smem.result.failure <q>
              ^smem.command <cmd>)
   (<o> ^name retrieve-predicate-word
        ^lookup-predicate <lp>)
   (<lp> ^predicate-query <q>)
   (<cmd> ^query <q>)
-->
   (<cmd> ^query <q> -)
   (<lp> ^word-lti lookup-failure)
}

sp {generate-object-description*apply*retrieve-predicate-word*create*word*query
   (state <s> ^name generate-object-description
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name retrieve-predicate-word
        ^lookup-predicate <lp>)
   (<lp> ^predicate-lti <lti>)
-->
   (<cmd> ^query <q>)
   (<q> ^id <lti>)
   (<lp> ^word-query <q>)
}

sp {generate-object-description*apply*retrieve-predicate-word*retrieve*word*success
   (state <s> ^name generate-object-description
              ^operator <o>
              ^smem.result.success <q>
              ^smem.result.retrieved <lti>
              ^smem.command <cmd>
              ^description <desc>)
   (<o> ^name retrieve-predicate-word
        ^lookup-predicate <lp>)
   (<lp> ^word-query <q>
         ^category <cat>)
   (<cmd> ^query <q>)
   (<lti> ^spelling <word>)
-->
   (<cmd> ^query <q> -)
   (<lp> ^word-lti <lti>)
   (<desc> ^<cat> <word>)
}

sp {generate-object-description*apply*retrieve-predicate-word*retrieve*word*failure
   (state <s> ^name generate-object-description
              ^operator <o>
              ^smem.result.failure <q>
              ^smem.command <cmd>)
   (<o> ^name retrieve-predicate-word
        ^lookup-predicate <lp>)
   (<lp> ^word-query <q>)
   (<cmd> ^query <q>)
-->
   (<cmd> ^query <q> -)
   (<lp> ^word-lti lookup-failure)
}