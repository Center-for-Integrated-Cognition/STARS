###
# valid-objects: objects that may appear in the diff

sp {state-diff*elaborate*valid-objects
   (state <s> ^name state-diff)
-->
#(interrupt)
   (<s> ^valid-objects <objs>)
}

sp {state-diff*elaborate*valid-object*from-final
   (state <s> ^name state-diff
              ^valid-objects <objs>
              ^final.objects.object <obj>
             -^ignore.object <obj>
             -^attend-only.objects.object <any>)
-->
   (<objs> ^object <obj>)
}

sp {state-diff*elaborate*valid-object*attend
   (state <s> ^name state-diff
              ^valid-objects <objs>
              ^attend.object <obj>)
-->
   (<objs> ^object <obj>)
}

sp {state-diff*elaborate*valid-object*attend-only
   (state <s> ^name state-diff
              ^valid-objects <objs>
              ^attend-only.objects.object <obj>)
-->
   (<objs> ^object <obj>)
}

###
# valid-relationships: relations that may appear in the diff

sp {state-diff*elaborate*valid-relations
   (state <s> ^name state-diff)
-->
   (<s> ^valid-relations <rels>)
}

sp {state-diff*elaborate*valid-relation*from-final
   (state <s> ^name state-diff
              ^valid-relations <rels>
              ^final.relationships.relations.name <rel>
             -^ignore.relation <rel>
             -^attend-only.relations.relation <any>)
-->
   (<rels> ^relation <rel>)
}

sp {state-diff*elaborate*valid-relation*attend-only
   (state <s> ^name state-diff
              ^valid-relations <rels>
              ^attend-only.relations.relation <rel>)
-->
   (<rels> ^relation <rel>)
}

###
# valid-predicates: predicates that may appear in the diff

sp {state-diff*elaborate*valid-predicates
   (state <s> ^name state-diff)
-->
   (<s> ^valid-predicates <preds>)
}

sp {state-diff*elaborate*valid-predicate*from-final
   (state <s> ^name state-diff
              ^valid-predicates <preds>
              ^final.predicate-set.predicate.name <pred>
             -^ignore.predicate <pred>
             -^attend-only.predicates.predicate <any>)
-->
   (<preds> ^predicate <pred>)
}

sp {state-diff*elaborate*valid-predicate*attend-only
   (state <s> ^name state-diff
              ^valid-predicates <preds>
              ^attend-only.predicates.predicate <pred>)
-->
   (<preds> ^predicate <pred>)
}

# copy new relationships
# copy relationships that are in the final state and not the initial

sp {state-diff*elaborate*copied-relations
   (state <s> ^name state-diff)
-->
   (<s> ^copied-relations <rels>)
}

sp {state-diff*propose*copy-new-relationships
   (state <s> ^name state-diff
             -^copied-relationships)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-new-relationships)
}

sp {state-diff*apply*copy-new-relationships*mark*copied-relatioships
   (state <s> ^name state-diff
              ^operator.name copy-new-relationships)
-->
   (<s> ^copied-relationships true)
}

# If the relation is in the attend set, 
# Copy all with all objects valid, without doing a diff
sp {state-diff*apply*copy-new-relationships*copy*attend*relation
   (state <s> ^name state-diff
              ^operator.name copy-new-relationships
              ^relationships <r>
              ^attend.relation <rel-name>
              ^final.relationships.relations <final-rel>
              ^valid-objects <objs>
              ^copied-relations <copied>)
   (<final-rel> ^name <rel-name>
                ^instance <instance>)
   # All objects on the instance are valid
   -{(<instance> ^{ << 1 2 3 >> } <obj>)
     (<objs> -^object <obj>)}
-->
   (<r> ^relations <new-rel>)
   (<new-rel> ^name <rel-name> 
              ^instance <instance>)
   (<copied> ^<rel-name> <instance>)
}

# If the relation is not in the attend set, 
#   Add if all objects are valid, and it is not in the intial set
sp {state-diff*apply*copy-new-relationships*copy*1arg
   (state <s> ^name state-diff
              ^operator.name copy-new-relationships
              ^relationships <r>
             -^attend.relation <rel-name>          
              ^final.relationships.relations <final-rel>
              ^valid-objects.object <ida>
              ^valid-relations.relation <rel-name>
              ^copied-relations <copied>)
   (<final-rel> ^name <rel-name>
                ^instance <instance>)
   (<instance> ^1 <ida> -^2)
   -{(<s> ^initial.relationships <initial-rels>)
     (<initial-rels> ^relations <initial-rel>)
     (<initial-rel> ^name <rel-name>
                    ^instance <initial-instance>)
     (<initial-instance> ^1 <ida>)}
-->
   (<r> ^relations <new-rel>)
   (<new-rel> ^name <rel-name> 
              ^instance <instance>)
   (<copied> ^<rel-name> <instance>)
}
sp {state-diff*apply*copy-new-relationships*copy*2arg
   (state <s> ^name state-diff
              ^operator.name copy-new-relationships
              ^relationships <r>
             -^attend.relation <rel-name>          
              ^final.relationships.relations <final-rel>
              ^valid-objects.object <ida>
              ^valid-objects.object <idb>
              ^valid-relations.relation <rel-name>
              ^copied-relations <copied>)
   (<final-rel> ^name <rel-name>
                ^instance <instance>)
   (<instance> ^1 <ida> ^2 <idb> -^3)
   -{(<s> ^initial.relationships <initial-rels>)
     (<initial-rels> ^relations <initial-rel>)
     (<initial-rel> ^name <rel-name>
                    ^instance <initial-instance>)
     (<initial-instance> ^1 <ida> ^2 <idb>)}
-->
   (<r> ^relations <new-rel>)
   (<new-rel> ^name <rel-name> 
              ^instance <instance>)
   (<copied> ^<rel-name> <instance>)
}
  
# If the relation is not in the attend set, 
#   Add if all objects are valid, and it is not in the intial set
sp {state-diff*apply*copy-new-relationships*copy*3arg
   (state <s> ^name state-diff
              ^operator.name copy-new-relationships
              ^relationships <r>
             -^attend.relation <rel-name>             
              ^final.relationships.relations <final-rel>
              ^valid-objects.object <ida>
              ^valid-objects.object <idb>
              ^valid-objects.object <idc>
              ^valid-relations.relation <rel-name>
              ^copied-relations <copied>)
   (<final-rel> ^name <rel-name>
                ^instance <instance>)
   (<instance> ^1 <ida> ^2 <idb> ^3 <idc>)
   -{(<s> ^initial.relationships <initial-rels>)
	 (<initial-rels> ^relations <initial-rel>)
     (<initial-rel> ^name <rel-name>
                    ^instance <initial-instance>)
     (<initial-instance> ^1 <ida> ^2 <idb> ^3 <idc>)}
-->
   (<r> ^relations <new-rel>)
   (<new-rel> ^name <rel-name> 
              ^instance <instance>)
   (<copied> ^<rel-name> <instance>)
}

#copy objects that appear in the new relationships
#or are in attend list, ignore objects from ignore list
sp {state-diff*propose*copy-objects
   (state <s> ^name state-diff
              ^copied-relationships true
             -^copied-objects)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-objects)
}

sp {state-diff*apply*copy-objects*mark*copied
   (state <s> ^name state-diff
              ^operator.name copy-objects)
-->
   (<s> ^copied-objects true)
}

# Copy an object if in the attend set
sp {state-diff*apply*copy-objects*copy*object*in*attend
   (state <s> ^name state-diff
              ^operator.name copy-objects
              ^attend.object <obj>
              ^final.objects.object <ocj>
              ^object-set <obj-set>)
             # ^object-desc-set <desc-set>)
-->
   (<obj-set> ^object <obj>)
 #  (<desc-set> ^obj-desc <desc>)
}

# Copy an object if it was in a copied-relation
sp {state-diff*apply*copy-objects*copy*object*from*relationship
   (state <s> ^name state-diff
              ^operator.name copy-objects
              ^object-set <obj-set>
#              ^object-desc-set <desc-set>
              ^copied-relations.<rel-name>.{<< 1 2 3 >>} <obj>
              ^final.objects.object <obj>)
              #^final.object-desc-set.obj-desc <desc>)
#   (<obj> ^id <id>)
#   (<desc> ^id <id>)
-->
   (<obj-set> ^object <obj>)
 #  (<desc-set> ^obj-desc <desc>)
}

# copy object predicates that changed (e.g. open to closed, raw to cooked)
sp {state-diff*propose*copy-predicates
   (state <s> ^name state-diff
             -^copied-predicates)
-->
   (<s> ^operator <o> + = >)
   (<o> ^name copy-predicates)
}

sp {state-diff*apply*copy-predicates*mark*done
   (state <s> ^name state-diff
              ^operator.name copy-predicates)
-->
   (<s> ^copied-predicates true)
}

sp {state-diff*apply*copy-predicates*object-descriptions
   (state <s> ^name state-diff
              #^operator.name copy-predicates
              ^predicate-set <preds>
              ^object-set.object <obj>
              ^valid-objects.object <obj>)
   (<obj> ^predicates.category <type>)
-->
   (<preds> ^<obj> <description>)
   (<description> ^type <type>)
}

#TODO umm all predicates get added??? will even use?
sp {state-diff*apply*copy-predicates*create*diff*changed
   (state <s> ^name state-diff
              ^operator.name copy-predicates
             # ^initial.predicate-set.predicate <initial-pred>
              ^final.predicate-set.predicate <final-pred>
              ^predicate-set <preds>
              ^object-set.object <obj>
              ^valid-predicates.predicate <name>
              ^valid-objects.object <obj>)
  # (<initial-pred> ^object <obj> ^name <name> ^value <val>)
   (<final-pred> ^object <obj> ^name <name> ^value <val2>)
    (<preds> ^<obj> <desc>)
-->
   (<preds> ^predicate <new-pred>)
   (<new-pred> ^object <obj> ^name <name> ^value <val2>)
   (<desc> ^<name> <val2>)
}

#copy non-new relationships that exist between objects in the new state
#TODO also copy  non-new relationships that are on attend list and involve atle
sp {state-diff*propose*copy-extra-relationships
   (state <s> ^name state-diff
              ^copied-objects true
             -^copied-extra-relationships)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-extra-relationships)
}

sp {state-diff*apply*copy-extra-relationships*default
   (state <s> ^name state-diff
              ^operator.name copy-extra-relationships)
-->
   (<s> ^copied-extra-relationships true)
}

sp {state-diff*apply*copy-extra-relationships*2args
   (state <s> ^name state-diff
              ^operator.name copy-extra-relationships
              ^relationships <rels>
              ^final.relationships.relations <final-rel>
              ^valid-relations.relation <rel-name>
              ^object-set.object <ida>
              ^object-set.object <idb>
              ^copied-relations <copied>)
   (<final-rel> ^name <rel-name> ^instance <instance>)
   (<instance> ^1 <ida> ^2 <idb> -^c)
	(<copied> -^<rel-name> <instance>)
-->
   (<rels> ^relations <new-rel>)
   (<new-rel> ^name <rel-name> ^instance <instance>)
   (<copied> ^<rel-name> <instance>)
}

sp {state-diff*apply*copy-extra-relationships*3args
   (state <s> ^name state-diff
              ^operator.name copy-extra-relationships
              ^relationships <rels>
              ^final.relationships.relations <final-rel>
              ^valid-relations.relation <rel-name>
              ^object-set.object <ida>
              ^object-set.object <idb>
              ^object-set.object <idc>
              ^copied-relations <copied>)
   (<final-rel> ^name <rel-name> ^instance <instance>)
   (<instance> ^1 <ida> ^2 <idb> ^c <idc>)
   (<copied> -^<rel-name> <instance>)
-->
   (<rels> ^relations <new-rel>)
   (<new-rel> ^name <rel-name> ^instance <instance>)
   (<copied> ^<rel-name> <instance>)
}

#complete, copy to output state-result
sp {state-diff*propose*complete
   (state <s> ^name state-diff)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete)
}

sp {state-diff*apply*complete
   (state <s> ^name state-diff
              ^operator.name complete
              ^object-set <os>
              ^relationships <rels>
#              ^object-desc-set <ods>
              ^predicate-set <preds>
#              ^extra-relationships <rels2>
              ^superstate.operator <op>
              ^superstate <ss>)
#  -{(<rels2> ^relations <rr>)
#    (<rr> ^name <name> ^instance <instance>)
#    -{(<rels> ^relations.instance <instance>)}}
-->
   (<ss> ^diff-result <res>)
   (<res> ^relationships <rels> ^objects <os> ^predicates <preds>)#^object-desc-set <ods>
}