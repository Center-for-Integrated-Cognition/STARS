#sp {ground-referent*elaborate*learn
#  #:interrupt
#   (state <s> ^name ground-referent
#             -^superstate.name evaluate-operator)
#-->
# (force-learn <s>)
#}

sp {ground-referent*elaborate*substate*count
   (state <s> ^name ground-referent
              ^superstate.operator <so>)
   (<so> ^name ground-referent
         ^constraint-count <cc>)
-->
   (<s> ^constraint-count <cc>
        ^candidate-set <cs>)
}

sp {ground-referent*elaborate*substate*constraints
   (state <s> ^name ground-referent
              ^superstate.operator <so>)
   (<so> ^name ground-referent
         ^constraints <cons>)
-->
   (<s> ^constraints <cons>)
}

sp {ground-referent*constraint-lt
   (state <s> ^name ground-referent
              ^constraints.lt <x>)
-->
   (<s> ^constraint-lt <x>)
}

sp {ground-referent*constraint-lt2
   (state <s> ^name ground-referent
              ^superstate.operator <o>)
   (<o> ^name ground-referent
        ^double-constraints.lt <x>)
-->
   (<s> ^constraint-lt <x>)
}
sp {ground-referent*elaborate*substate*constraint
   (state <s> ^name ground-referent
              ^superstate.operator <so>)
   (<so> ^name ground-referent
         ^constraints.constraint <con>)
-->
   (<s> ^constraint <con>)
}

sp {ground-referent*elaborate*substate0
  #:interrupt
   (state <s> ^name ground-referent
              ^constraint <con>)
   (<con> ^name <value>
          ^property.name <name>)
-->
   (<s> ^property-constraint <pc>)
   (<pc> ^property <name> 
         ^value <value>)
}

sp {ground-referent*elaborate*substate1
   #:interrupt
   (state <s> ^name ground-referent
              ^constraint <con>)
   (<con> ^referent.handle <value>)
-->
   (<s> ^property-constraint <pc>)
   (<pc> ^property handle
         ^value <value>)
}

sp {ground-referent*elaborate*substate2
   (state <s> ^name ground-referent
              ^constraint <con>)
   (<con> ^property <prop>
          ^handle <value>)
   (<prop> ^<< name handle >> <property>)
-->
   (<s> ^property-constraint <pc>)
   (<pc> ^property <property>
         ^value <value>)
}

# AM: Commented out, all categories are elaborated on objects so 
#       the supercategory will ground correctly
#sp {ground-referent*elaborate*substate2*subcategory
#   (state <s> ^name ground-referent
#              ^constraint <con>
#              ^property-constraint <pc>)
#   (<pc> ^property <property>
#         ^value <value>)
#   (<con> ^property <prop>
#          ^handle <value>
#           ^subcategory.handle <x>)
#   (<prop> ^<< name handle >> <property>)
#-->
#   (<pc> ^value <x>)
#}

sp {ground-referent*elaborate*substate3
   (state <s> ^name ground-referent
              ^constraint <con>)
   (<con> ^property <p>)
   (<p> ^handle <property>
        ^value <value>)
-->
   (<s> ^property-constraint <pc>)
   (<pc> ^property <property>
         ^value <value>)
}

sp {ground-referent*elaborate*substate4
 #:interrupt
   (state <s> ^name ground-referent
              ^constraint <con>)
   (<con> ^{ << movable category type >> <type> } <value>)
-->
   (<s> ^property-constraint <pc>)
   (<pc> ^property <type>
         ^value <value>)
}
##################
sp {ground-referent*elaborate*constraint-processing-finished
   (state <s> ^name ground-referent
              ^candidate-set.proto-referent.constraint-count 0)
-->
   (<s> ^constraint-processing-finished true)
}

sp {ground-referent*elaborate*proto-referents*none
   (state <s> ^name ground-referent              
              ^candidate-set <cs>)
   (<cs> ^generated-candidates true
        -^proto-referent.object <obj>)
-->
   (<s> ^proto-referents none)}

sp {ground-referent*elaborate*no-proto-referents*one
   (state <s> ^name ground-referent                
              ^candidate-set <cs>)
   (<cs> ^generated-candidates true
         ^proto-referent.object <obj>
        -^proto-referent.object <> <obj>)
   -->
   (<s> ^proto-referents one)}

sp {ground-referent*elaborate*no-proto-referents*multiple
   (state <s> ^name ground-referent                 
              ^candidate-set <cs>
             -^proto-referents none)
   (<cs> ^generated-candidates true)
  -{(<cs> ^proto-referent.object <obj>
         -^proto-referent.object <> <obj>)}
   -->
   (<s> ^proto-referents multiple)}

