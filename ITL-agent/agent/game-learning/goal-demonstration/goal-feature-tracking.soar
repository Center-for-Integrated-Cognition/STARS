sp {goal-feature-tracking*elaborate*parameter
   (state <s> ^name goal-feature-tracking
              ^superstate.operator <o>)
   (<o> ^name goal-feature-tracking
        ^parameter <pa>)
-->
   (<s> ^parameter <pa>)
}
sp {goal-feature-tracking*propose*add-to-list
   (state <s> ^name goal-feature-tracking
             -^done
              ^parameter <pa>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name add-to-list)
}
sp {goal-feature-tracking*propose*complete
   (state <s> ^name goal-feature-tracking
              ^done
              ^parameter <pa>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name complete)
}

sp {goal-feature-tracking*apply*add-to-list*object
   (state <s> ^name goal-feature-tracking
              ^parameter <pa>
              ^top-state.current-game <cg>
              ^operator.name add-to-list)
   (<pa> ^type <attig> -^relation ^indexable.result <o>)
   (<cg> ^<attig> <at>)
-->
   (<at> ^object <o>)
   (<s> ^done true)
}

sp {goal-feature-tracking*apply*add-to-list*relation
   (state <s> ^name goal-feature-tracking
              ^parameter <pa>
              ^top-state.current-game <cg>#JK outdated
              ^operator.name add-to-list)
   (<pa> ^type <attig> ^relation <r>)
   (<cg> ^<attig> <at>)
-->
   (<at> ^relation <r>)
   (<s> ^done true)
}

sp {goal-feature-tracking*apply*complete
   (state <s> ^name goal-feature-tracking
              ^parameter <pa>
              ^operator.name complete
              ^top-state.io.output-link <out>
              ^top-state.interaction.status <status>)
-->
   (<out> ^send-message <msg>)
   (<msg> ^type confirmation
          ^fields <f>)
   (<status> ^performed.dialog-event.type game)
}