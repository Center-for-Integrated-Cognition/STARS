sp {learn-predicate-info*propose*update-smem
   (state <s> ^name learn-predicate-info
              ^predicate-id <pred>
              ^property-id <prop>)
-->
   (<s> ^operator <o> +)
   (<o> ^name update-smem)
}

sp {learn-predicate-info*apply*update-smem*create*property*link
   (state <s> ^name learn-predicate-info
              ^operator.name update-smem
              ^predicate-id <pred>
              ^property-id <prop>)
-->
   (<pred> ^property <prop>)
}

sp {learn-predicate-info*apply*update-smem*store
   (state <s> ^name learn-predicate-info
              ^operator.name update-smem
              ^smem.command <cmd>
              ^predicate-id <pred>)
-->
   (<cmd> ^store <pred>)
}

sp {learn-predicate-info*apply*update-smem*stored*mark*performed
   (state <s> ^name learn-predicate-info
              ^operator.name update-smem
              ^predicate-id <pred>
              ^smem.result.success <pred>
              ^top-state.interaction.status <status>)
-->
   (<status> ^performed.learning-event.type predicate)
}