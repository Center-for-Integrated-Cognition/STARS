### COPY STATUS PREDICATE

sp {task-utils*copy-task-argument*expand*status*predicate*current-locaiton
   (state <s> ^copy-task-argument <arg>)
   (<arg> ^arg-type predicate
          ^source-arg <src>
          ^object-map <map>)
   (<src> ^type status
          ^name current-location
          ^1 <obj1>)
-->
   (<arg> ^obj1-copy <obj1-copy>)

   (<s> ^copy-task-argument <obj1-copy>)
   (<obj1-copy> ^arg-type object
                ^source-arg.id <obj1>
                ^object-map <map>)
}

sp {task-utils*copy-task-argument*expand*status*predicate*current-waypoint
   (state <s> ^copy-task-argument <arg>)
   (<arg> ^arg-type predicate
          ^source-arg <src>)
   (<src> ^type status
          ^name current-waypoint
          ^1 <wp1>)
-->
   (<arg> ^obj1-copy <obj1-copy>)

   (<s> ^copy-task-argument <obj1-copy>)
   (<obj1-copy> ^arg-type waypoint
                ^source-arg.id <wp1>)
}

sp {task-utils*copy-task-argument*propose*copy-task-argument*status*predicate
   (state <s> ^copy-task-argument <arg>)
   (<arg> ^arg-type predicate
          ^source-arg <src>
          ^obj1-copy.arg-copy.id <obj1>
         -^arg-copy)
   (<src> ^type status
          ^name <status-name>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-task-argument
        ^argument <arg>
        ^arg-type predicate
        ^pred-type status
        ^status-name <status-name>
        ^obj1 <obj1>)
}

sp {task-utils*copy-task-argument*apply*copy-task-argument*status*predicate
   (state <s> ^operator <o>)
   (<o> ^name copy-task-argument
        ^argument <arg>
        ^arg-type predicate
        ^pred-type status
        ^status-name <status-name>
        ^obj1 <obj1>)
-->
   (<arg> ^arg-copy <copy>)
   (<copy> ^type status
           ^name <status-name>
           ^1 <obj1>)
}


