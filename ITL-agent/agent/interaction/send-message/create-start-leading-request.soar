sp {send-message*start-leading-request*elaborate*object-description*person
   (state <s> ^name send-message
              ^message.type start-leading-request
              ^message.person <person>)
-->
   (<s> ^object-description <desc>)
   (<desc> ^object <person>)
}

sp {send-message*propose*create-start-leading-request
   (state <s> ^name send-message
              ^message.type start-leading-request
              ^message.person <person>
              ^object-description <desc>
             -^created-message)
   (<desc> ^object <person>
           ^description <person-desc>)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-start-leading-request
        ^person <person-desc>)
}

sp {send-message*apply*create-start-leading-request
   (state <s> ^name send-message
              ^operator <o>)
   (<o> ^name create-start-leading-request
        ^person <person>)
-->
   (<s> ^created-message <msg>)
   (<msg> ^type start-leading-request
          ^fields.person <person>)
}
