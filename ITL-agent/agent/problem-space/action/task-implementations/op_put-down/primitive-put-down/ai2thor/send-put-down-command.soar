sp {primitive-put-down*ai2thor*propose*send-put-down-command
   (state <s> ^name primitive-put-down
              ^agent-params.domain ai2thor
              ^problem-space.execution-type external
             -^current-task-segment.put-down-command
              ^held-object <obj>
              ^destination <dest>)
-->
   (<s> ^operator <o> +)
   (<o> ^name send-put-down-command
        ^object <obj>
        ^destination <dest>)
}

sp {primitive-put-down*ai2thor*apply*send-put-down-command*send
   (state <s> ^name primitive-put-down
              ^agent-params.domain ai2thor
              ^problem-space.execution-type external
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-put-down-command
        ^object.handle <obj-h>
        ^destination.handle <dest-h>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^name put-down
         ^object <obj-h>
         ^receptacle <dest-h>)
   (<seg> ^put-down-command <pa>)
}
