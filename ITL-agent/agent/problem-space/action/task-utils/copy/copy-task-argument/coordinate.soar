### COPY COORDINATE ARGUMENT

sp {task-utils*copy-task-argument*propose*copy-task-argument*coordinate
   (state <s> ^copy-task-argument <arg>)
   (<arg> ^arg-type coordinate
          ^source-arg.coord <coord>
         -^arg-copy)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-task-argument
        ^argument <arg>
        ^arg-type coordinate
        ^coord <coord>)
}

sp {task-utils*copy-task-argument*apply*copy-task-argument*coordinate*xy
   (state <s> ^operator <o>)
   (<o> ^name copy-task-argument
        ^argument <arg>
        ^arg-type coordinate
        ^coord <coord>)
   (<coord> ^x <x> ^y <y> -^z <z>)
-->
   (<arg> ^arg-copy <copy>)
   (<copy> ^coord <coord-copy>)
   (<coord-copy> ^x <x> ^y <y>)
}

sp {task-utils*copy-task-argument*apply*copy-task-argument*coordinate*xyz
   (state <s> ^operator <o>)
   (<o> ^name copy-task-argument
        ^argument <arg>
        ^arg-type coordinate
        ^coord <coord>)
   (<coord> ^x <x> ^y <y> ^z <z>)
-->
   (<arg> ^arg-copy <copy>)
   (<copy> ^coord <coord-copy>)
   (<coord-copy> ^x <x> ^y <y> ^z <z>)
}

