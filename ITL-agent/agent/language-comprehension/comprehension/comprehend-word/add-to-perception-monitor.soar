sp {comprehend-word*propose*add-to-perception-monitor
   (state <s> ^name comprehend-word #comprehend-construction
             -^top-state.game-scripting true
              ^current-word <cw>)
   (<s> ^retrieved-lexical-item.relation <ri>
        -^failed-retrieval true
        -^added-to-monitor true
        -^segment.retry)
   (<ri> ^handle <rhandle> ^type relation -^handle of1 )
-->
   (<s> ^operator <o> + >)
   (<o> ^name add-to-perception-monitor
        ^relation-handle <rhandle>
        ^retrieved <ri>)
}

sp {comprehend-word*propose*add-to-perception-monitor*linear
   (state <s> ^name comprehend-word #comprehend-construction
              -^top-state.game-scripting true
              ^current-word.spelling linear)
    (<s> ^retrieved-lexical-item.referent <ri>
         -^failed-retrieval true
        -^added-to-monitor true
        -^segment.retry)
-->
   (<s> ^operator <o> + >)
   (<o> ^name add-to-perception-monitor
        ^relation-handle linear
        ^retrieved <ri>)
}

### Retrieval
sp {comprehend-word*apply*add-to-perception-monitor
   (state <s> ^name comprehend-word
              ^top-state.perception-monitor.predicate-monitor <pm>
              ^operator <o>)
   (<o> ^name add-to-perception-monitor
        ^relation-handle <rhandle>
        ^retrieved <ri>)
   (<pm> -^predicate-info.predicate-handle <rhandle>)
--> 
   (<pm> ^predicate-info <pi>)
   (<pi> ^predicate-handle <rhandle> ^smem-info <ri>)
}

sp {comprehend-word*apply*add-to-perception-monitor*complete
   (state <s> ^name comprehend-word
              ^top-state.perception-monitor.predicate-monitor <pm>
              ^operator <o>)
   (<o> ^name add-to-perception-monitor
        ^relation-handle <rhandle>
        ^retrieved <ri>)
   (<pm> ^predicate-info.predicate-handle <rhandle>)
--> 
    (<s> ^added-to-monitor true)
}

