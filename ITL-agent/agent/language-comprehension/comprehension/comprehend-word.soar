sp {comprehension*propose*comprehend-word
   (state <s> ^name comprehension
              ^segment <seg>)
   (<seg> ^current-word <cw>)
   (<cw> -^processed true)
-->
   (<s> ^operator <o> +, =)
   (<o> ^name comprehend-word
        ^current-word <cw>)
}

sp {comprehension*propose*comprehend-word*retry
   (state <s> ^name comprehension
              ^segment.retry <retry>)
   (<retry> ^current-word <cw>)
-->
   (<s> ^operator <o> +, =)
   (<o> ^name comprehend-word
        ^current-word <cw>
        ^retry <retry>)
}

sp {apply*comprehend-word*skip-*
   (state <s> ^operator <op>
              ^segment <seg>)
   (<op> ^name comprehend-word)
   (<seg> ^current-word <cw>)
   (<cw> ^spelling |*|)
-->
   #(write (crlf) |Skip *.|)
   (<seg> ^comprehension-structure <cs>)
   (<cw> ^processed true)
   (<cs> ^processed true)
}

sp {comprehension*compare*comprehend-word
   (state <s> ^name comprehension
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name comprehend-word)
   (<op2> ^name process-phrase-end)
-->
   (<s> ^operator <op1> > <op2>)
}
