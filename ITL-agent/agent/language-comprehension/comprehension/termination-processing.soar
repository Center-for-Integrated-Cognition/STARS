sp {comprehension*propose*termination-processing
   (state <s> ^name comprehension
              ^segment <seg>)
   (<seg> -^processed-terminator
          -^parse-failed-receiver
          -^parse-failed-assigner
           ^not-merged-receiver <ua>)
   (<ua> ^terminator <term>)
        # ^predicate <VP>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name termination-processing)
}

sp {apply*termination-processing*message-type
   (state <s> ^operator.name termination-processing
              ^segment <seg>)
   (<seg> ^semantics <sem>
          ^not-merged-receiver.lt <lt>)
   (<lt> ^message-type <ms-type>
         ^structure-type S)
-->
   (<sem> ^message-type <ms-type>)
}


#sp {apply*termination-processing*semantics*sentence-semantics
#   (state <s> ^operator.name termination-processing
#              ^segment <seg>)
#   (<seg> ^semantics <sem>
#          ^not-merged-receiver <uri>)
#   (<uri> ^semantics.sentence-semantics <ss>
#          ^lt.structure-type S)
#   (<ss> ^{ <> sentence-semantics <att> } <val>)
#-->
#   (<sem> ^<att> <val>)
#}

sp {apply*termination-processing*semantics
   (state <s> ^operator.name termination-processing
              ^segment <seg>)
   (<seg> ^semantics <sem>
          ^not-merged-receiver <uri>)
   (<uri> ^semantics <sem2>
          ^lt.structure-type S)
   (<sem2> ^<att> <val>)
-->
   (<sem> ^<att> <val>)
}


sp {apply*termination-processing*semantics*sentence
   (state <s> ^operator.name termination-processing
              ^segment <seg>)
   (<seg> ^semantics <sem>
          ^not-merged-receiver <uri>)
   (<uri> ^sentence.semantics <sem2>
          ^lt.structure-type S)
   (<sem2> ^<att> <val>)
-->
   (<sem> ^<att> <val>)
}

sp {apply*termination-processing*message-type*st
#:interrupt
   (state <s> ^operator.name termination-processing
              ^segment <seg>)
   (<seg> ^semantics <sem>
          ^not-merged-receiver <rec>)
   (<rec> ^message-type <ms-type>
          ^structure-type S)
-->
   (<sem> ^message-type <ms-type>)
}

sp {comprehension*compare*termination-processing
   (state <s> ^operator <op1> +
                        <op2> +)
   (<op1> ^name termination-processing)
   (<op2> ^name comprehend-construction)
-->
   (<s> ^operator <op1> > <op2>)
}

  