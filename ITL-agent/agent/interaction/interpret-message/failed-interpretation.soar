# When a message is not successfully interpreted, there will be an op no-change
#   In the substate, we report a failure to the top-state and retract the operator
sp {interpret-message*op-no-change*propose*report-failure
   (state <s> ^superstate.name interpret-message
             -^allow-substate true
              ^attribute operator
              ^impasse no-change)
-->
   (<s> ^operator <o> + >)
   (<o> ^name report-failure)
}

sp {interpret-message*op-no-change*apply*report-failure
   (state <s> ^superstate.name interpret-message
              ^operator.name report-failure
             -^top-state.dialog-context.learning true #JK handle unknown words differently for game learning currently
              ^top-state.interaction.status <status>)
-->
   (<status> ^failure.type failed-to-interpret-message)
}

sp {interpret-message*op-no-change*apply*report-failure*reject
   (state <s> ^superstate.name interpret-message
              ^top-state.dialog-context.learning true #JK handle unknown words differently for game learning currently
              ^operator.name report-failure
              ^superstate <ss>)
   (<ss> ^operator <o>)
-->
   (<ss> ^operator <o> -)
}

sp {interpret-message*op-no-chnage*apply*report-failure*remove*message
    (state <s> ^superstate.name interpret-message
              -^top-state.dialog-context.learning true #JK handle unknown words differently for game learning currently
               ^operator.name report-failure
               ^superstate.instructor-message <msg>
				       ^top-state.interaction.status <status>)
	  (<status> ^new-message <msg>)
 -->
    (<status> ^new-message <msg> -)
 }
