sp {ground-referent*propose*index-scene-relation
   (state <s> ^name ground-referent
              ^candidate-set <cs>
              ^constraint.relation <r>)
   (<cs> ^generated-candidates true
        -^satisfied-property <r>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name index-object-scene-relation
         ^relation-constraint <r>)
}
sp {ground-referent*apply*index-object-scene-relation*violates
   (state <s> ^operator <o>
             ^candidate-set <cs>)
   (<cs> ^proto-referent <pr>)   
   (<pr> ^object <r-1>)
   (<o> ^name index-object-scene-relation
        ^relation-constraint <rc>)
   (<rc> ^2 <r-2>
         ^handle <sr>)
 -{(<s> ^top-state.world.predicates.predicate <wsr>)
   (<wsr> ^handle <sr>
          ^instance <pwsr>)
   (<pwsr> ^1 <r-1>
           ^2 <r-2>)}
-->
   (<pr> ^violates <rc>)
   (<cs> ^fail-tested <rc>)
}

sp {apply*index-object-scene-relation*passes
   (state <s> ^operator <op>
              ^top-state.world.predicates.predicate <wsr>
              ^candidate-set <cs>)
   (<cs> ^proto-referent <pr>)   
   (<pr> ^object <r-1>)
   (<op> ^name index-object-scene-relation
         ^relation-constraint <rc>)
   (<rc> ^2 <r-2>
          ^handle <sr>)
   (<wsr> ^handle <sr>
          ^instance <pwsr>)
   (<pwsr> ^1 <r-1>
           ^2 <r-2>)

-->
   (<pr> ^passes <rc>)
   (<cs> ^success-tested <rc>)
   #(write (crlf) |Successful relation grounding: | <r-1> | | <sr> | | <r-2>)
}

sp {ground-referent*apply*index-object-scene-relation*mark*checked
   (state <s> ^candidate-set <cs>
              ^operator <o>)
   (<o> ^name index-object-scene-relation
        ^relation-constraint <pc>)
   (<cs> ^success-tested <pc>)
-->
   (<cs> ^satisfied-property <pc>)
}

sp {ground-referent*apply*index-object-scene-relation*mark*checked*failed
   (state <s> ^candidate-set <cs>
              ^operator <o>)
   (<o> ^name index-object-scene-relation
        ^relation-constraint <pc>)
   (<cs> -^success-tested <pc>
         ^fail-tested <pc>)
-->
   (<cs> ^satisfied-property <pc>)
}

sp {ground-referent*apply*index-object-scene-relationmark*satisfied
   (state <s> ^candidate-set.proto-referent <pr>
              ^operator <o>)
   (<o> ^name index-object-scene-relation
        ^relation-constraint <pc>)
   (<pr> ^passes <pc>
         ^constraint-count <cc>)
-->
   (<pr> ^constraint-count <cc> - (- <cc> 1))
}

sp {ground-referent*apply*index-object-scene-relation*remove-violated
   (state <s> ^candidate-set <cs>
              ^operator <o>)
   (<o> ^name index-object-scene-relation
        ^relation-constraint <pc>)
   (<cs> ^proto-referent <pr>)
   (<pr> ^violates <pc>)
-->
   (<cs> ^proto-referent <pr> -)
}
