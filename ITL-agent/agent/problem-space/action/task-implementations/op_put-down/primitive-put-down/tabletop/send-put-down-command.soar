sp {primitive-put-down*tabletop*propose*send-put-down-command
   (state <s> ^name primitive-put-down
              ^agent-params.domain tabletop
              ^problem-space.execution-type external
              ^projection-result <res>
              ^world.robot.arm.moving-status wait
             -^current-task-segment.put-down-command)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-put-down-command
        ^destination <res>)
}

# Put the command on the output link
sp {primitive-put-down*tabletop*apply*send-put-down-command*send
   (state <s> ^name primitive-put-down
              ^agent-params.domain tabletop
              ^problem-space.execution-type external
              ^operator <o>
              ^io.output-link <out>
              ^current-task-segment <seg>)
   (<o> ^name send-put-down-command
        ^destination <dest>)
   (<dest> ^x <x> ^y <y> ^z <z>)
-->
   (<out> ^put-down <cmd>)
   (<cmd> ^location <loc>)
   (<loc> ^x <x> ^y <y> ^z <z>)
   (<seg> ^put-down-command <cmd>)
}

# Relocate the belief object in svs
sp {primitive-put-down*tabletop*apply*send-put-down-command*relocate*object
   (state <s> ^name primitive-put-down
              ^agent-params.domain tabletop
              ^problem-space.execution-type external
              ^operator <o>
              ^held-object.svs-obj.id <id>
              ^top-state.svs.command <cmd>)
   (<o> ^name send-put-down-command
        ^destination <dest>)
   (<dest> ^x <x> ^y <y> ^z <z>)
-->
   (write |Moving object to tracked location| (crlf))
   (<cmd> ^set_transform <setxform>)
   (<setxform> ^id <id>
               ^position <pos>)
   (<pos> ^x <x> ^y <y> ^z <z>)
}

              
