### GENERALIZE WAYPOINT

# procedural match - the waypoint matches one on the reference-task-op
sp {task-utils*generalize-task-argument*propose*generalize-task-argument*waypoint*procedural
  (state <s> ^generalize-task-argument <arg>
             ^reference-tcn-proc.<arg-name> <parg>
             ^reference-task-op.<arg-name>.id <wp>)
  (<arg> ^arg-type waypoint
         ^instantiation.id <wp>
        -^general-arg-rep)
  (<parg> ^arg-type waypoint
          ^id <wp-slot>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type waypoint
       ^match-type procedural
       ^id <wp-slot>)
}

sp {task-utils*generalize-task-argument*apply*generalize-task-argument*waypoint*procedural
  (state <s> ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type waypoint
       ^match-type procedural
       ^id <wp-slot>)
-->
  (<arg> ^general-arg-rep <gen>)
  (<gen> ^id <wp-slot>)
}

# default match - copy the predicates onto a default rep
sp {task-utils*generalize-task-argument*propose*generalize-task-argument*waypoint*default
  (state <s> ^generalize-task-argument <arg>)
  (<arg> ^arg-type waypoint
         ^instantiation.id <wp>
        -^general-arg-rep)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type waypoint
       ^match-type default
       ^waypoint <wp>)
}

sp {task-utils*generalize-task-argument*apply*generalize-task-argument*waypoint*default
  (state <s> ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type waypoint
       ^match-type default
       ^waypoint.handle <wp-h>)
-->
  (<arg> ^general-arg-rep <rep>)
  (<rep> ^id <wp-slot>)
  (<wp-slot> ^default <def>)
  (<def> ^waypoint-handle <wp-h>)
}
