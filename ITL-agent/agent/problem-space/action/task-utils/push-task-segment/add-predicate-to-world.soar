sp {task-utils*push-task-segment*propose*add-predicate-to-world
   (state <s> ^name push-task-segment
              ^task-operator <task-op>
              ^world.predicates.predicate <ts-pred>
              ^world.objects <objs>
              ^predicate-lti <lti>)
   (<task-op> ^<arg1-name> <arg1>
              ^<arg2-name> <arg2>)
   (<arg1> ^arg-type object
           ^id <obj1>)
   (<arg2> ^arg-type partial-predicate
           ^handle <pred-handle>
           ^2 <obj2>)
   (<lti> ^handle <pred-handle>
          ^relation-type existing
         -^spatial-relation)
   (<objs> ^object <obj1> <obj2>)
   (<ts-pred> ^handle <pred-handle>)
   -{(<ts-pred> ^instance <i>)
     (<i> ^1 <obj1> ^2 <obj2>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-predicate-to-world
        ^handle <pred-handle>
        ^1 <obj1>
        ^2 <obj2>)
}

sp {task-utils*push-task-segment*propose*add-predicate-to-world*spatial-relation
   (state <s> ^name push-task-segment
              ^task-operator <task-op>
              ^world.predicates.predicate <ts-pred>
              ^world.objects <objs>
              ^predicate-lti <lti>)
   (<task-op> ^<arg1-name> <arg1>
              ^<arg2-name> <arg2>)
   (<arg1> ^arg-type object
           ^id <obj1>)
   (<arg2> ^arg-type partial-predicate
           ^handle <pred-handle>
           ^2 <obj2>)
   (<lti> ^handle <pred-handle>
          ^relation-type existing
          ^spatial-relation.handle <sr-handle>)
   (<objs> ^object <obj1> <obj2>)
   (<ts-pred> ^handle <sr-handle>)
   -{(<ts-pred> ^instance <i>)
     (<i> ^1 <obj1> ^2 <obj2>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-predicate-to-world
        ^handle <sr-handle>
        ^1 <obj1>
        ^2 <obj2>)
}

sp {task-utils*push-task-segment*apply*add-predicate-to-world
   (state <s> ^name push-task-segment
              ^operator <o>
              ^world.predicates.predicate <ts-pred>)
   (<o> ^name add-predicate-to-world
        ^handle <pred-handle>
        ^1 <obj1>
        ^2 <obj2>)
-->
   (<ts-pred> ^instance <i>)
   (<i> ^1 <obj1> ^2 <obj2>)
}

