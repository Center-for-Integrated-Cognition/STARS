sp {dialog-event*propose*answer-is-question
   (state <s> ^name dialog-event
              ^purpose.type is-question)
-->
   (<s> ^operator <o> +)
   (<o> ^name answer-is-question)
}

sp {answer-is-question*elaborate*relation-question
   (state <s> ^name answer-is-question
              ^superstate.purpose.parameters.relation-question <rel>)
-->
   (<s> ^relation-question <rel>)
}

sp {dialog-event*elaborate*answer-is-question*questioned
   (state <s> ^name dialog-event 
              ^operator <o> +
             -^purpose.parameters.relation-question
              ^purpose.parameters.questioned <adj>
              ^purpose.parameters.indexable <i>)
   (<o> ^name answer-is-question)
   (<i> ^type object
        ^indexable.result <word>)
   (<word> ^word <adj>)
-->
   (<o> ^questioned <word>)
}

sp {dialog-event*elaborate*answer-is-question*indexed-object
   (state <s> ^name dialog-event
              ^operator <o> +
              ^purpose.parameters.indexable <i>)
   (<o> ^name answer-is-question)
   (<i> ^type object
        ^result <obj>)
-->
   (<o> ^indexed-object <obj>)
}

sp {dialog-event*elaborate*answer-is-question*yes*word*match
   (state <s> ^name dialog-event
              ^operator <o> +)
   (<o> ^name answer-is-question
        ^indexed-object <obj>
        ^questioned.sense.label <label>)
   (<obj> ^property.label <label>)
-->
   (<o> ^answer yes)
}

sp {dialog-event*elaborate*answer-is-question*yes*generic
   (state <s> ^name dialog-event
              ^operator <o> +)
   (<o> ^name answer-is-question
        ^questioned.sense.property.name generic)
-->
   (<o> ^answer yes)
}

sp {dialog-event*elaborate*answer-is-question*yes*name
   (state <s> ^name dialog-event
              ^operator <o> +)
   (<o> ^name answer-is-question
        ^questioned.sense.property.name name
        ^questioned.word <name>
        ^indexed-object <obj>)
   (<obj> ^property.word <name>)
-->
   (<o> ^answer yes)
}

sp {dialog-event*elaborate*answer-is-question*default*no
   (state <s> ^name dialog-event
              ^operator <o> +)
   (<o> ^name answer-is-question
        ^questioned <q>
       -^answer yes)
-->
   (<o> ^answer no)
}

sp {dialog-event*apply*answer-is-question*send*answer
   (state <s> ^name dialog-event
              ^operator <o>
              ^io.output-link <out>
              ^top-state.interaction.status <status>)
   (<o> ^name answer-is-question
        ^answer <ans>)
-->
   (<out> ^send-message <msg>)
   (<msg> ^type single-word-response
          ^fields.word <ans>)
   (<status> ^performed.dialog-event <de>)
   (<de> ^type single-word-response
         ^originator agent)
}

sp {answer-is-question*propose*find-relations
   (state <s> ^name answer-is-question
              ^relation-question <q>)
   (<q> ^preposition <prep>
        ^object1.result.id <id1>)
-->
   (<s> ^operator <o> +)
   (<o> ^name find-relations
        ^prep <prep>
        ^obj <id1>
        ^ind 1)
}

sp {answer-is-question*apply*find-relations*yes
   (state <s> ^name answer-is-question
              ^operator.name find-relations
              ^relation-question <q>
              ^relationships <rels>
              ^top-state.interaction.status <status>
              ^io.output-link <out>)
   (<q> ^preposition <prep>
        ^object1.result.id <id1>
        ^object2.result.id <id2>)
   (<rels> ^preposition <prep>
           ^with <id1>
           ^relations.obj <id2>)
-->
   (<out> ^send-message <msg>)
   (<msg> ^type single-word-response
          ^fields.word yes)
   (<status> ^performed.dialog-event <de>)
   (<de> ^type single-word-response
         ^originator agent)
}

sp {answer-is-question*apply*find-relations*no
   (state <s> ^name answer-is-question
              ^operator.name find-relations
              ^relation-question <q>
              ^relationships <rels>
              ^top-state.interaction.status <status>
              ^io.output-link <out>)
   (<q> ^preposition <prep>
        ^object1.result.id <id1>
        ^object2.result.id <id2>)
   (<rels> ^preposition <prep>
           ^with <id1>
          -^relations.obj <id2>)
-->
   (<out> ^send-message <msg>)
   (<msg> ^type single-word-response
          ^fields.word no)
   (<status> ^performed.dialog-event <de>)
   (<de> ^type single-word-response
         ^originator agent)
}
   
sp {answer-is-question*apply*store*object
   (state <s> ^name answer-is-question
              ^operator.name find-relations
              ^top-state.smem.command <cmd>
              ^relation-question.object1.result.smem-rep <obj>)
-->
   (<cmd> ^store <obj>)
}
