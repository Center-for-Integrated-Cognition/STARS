# Interaction ordering
# 1. status-type failure
# 2. status-type new-message
# 3. change-type outgoing-message
# 4. change-type terminate-segment 
# 5. status-type performed-event
# 6. change-type clear-interaction-stack
# 7. change-type new-segment-info

sp {interaction*reject*new-message*if*performed*process-sentence
   (state <s> ^superstate nil
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interaction
         ^status-type performed
         ^status-info.dialog-event.type process-sentence)
   (<o2> ^name interaction
         ^status-type new-message)
-->
   (<s> ^operator <o2> -)
   }

              

# 1. failure
sp {interaction*better-preferences*failure
   (state <s> ^top-state <s>
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interaction
         ^status-type failure)
   (<o2> ^name interaction
         ^<type> << new-message outgoing-message performed clear-interaction-stack terminate-segment new-segment-info >> )
-->
   (<s> ^operator <o1> > <o2>)
}

# 2. terminate-segment
sp {interaction*better-preferences*terminate-segment
   (state <s> ^top-state <s>
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interaction
         ^change-type terminate-segment)
   (<o2> ^name interaction
         ^<type> << new-message outgoing-message performed clear-interaction-stack new-segment-info >> )
-->
   (<s> ^operator <o1> > <o2>)
}

# 3. new-message
sp {interaction*better-preferences*new-message
   (state <s> ^top-state <s>
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interaction
         ^status-type new-message)
   (<o2> ^name interaction
         ^<type> << outgoing-message performed clear-interaction-stack new-segment-info >> )
-->
   (<s> ^operator <o1> > <o2>)
}

# 4. outgoing-message
sp {interaction*better-preferences*outgoing-message
   (state <s> ^top-state <s>
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interaction
         ^change-type outgoing-message)
   (<o2> ^name interaction
         ^<type> << performed clear-interaction-stack new-segment-info >> )
-->
   (<s> ^operator <o1> > <o2>)
}

# 5. performed
sp {interaction*better-preferences*performed
   (state <s> ^top-state <s>
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interaction
         ^status-type performed)
   (<o2> ^name interaction
         ^<type> << clear-interaction-stack new-segment-info >> )
-->
   (<s> ^operator <o1> > <o2>)
}

sp {interaction*better-preferences*performed*process-sentence
   (state <s> ^top-state <s>
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interaction
         ^status-info.dialog-event.type process-sentence
         ^status-type performed)
   (<o2> ^name interaction
        -^status-info.dialog-event.type process-sentence
         ^status-type performed)
-->
   (<s> ^operator <o1> > <o2>)
}

# 6. clear-interaction-stack
sp {interaction*better-preferences*clear-interaction-stack
   (state <s> ^top-state <s>
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interaction
         ^change-type clear-interaction-stack)
   (<o2> ^name interaction
         ^<type> << new-segment-info >> )
-->
   (<s> ^operator <o1> > <o2>)
}

# 7. new-segment-info
