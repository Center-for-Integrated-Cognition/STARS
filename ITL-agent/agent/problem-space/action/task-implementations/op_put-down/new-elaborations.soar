sp {op_put-down1*elaborate*held-object*task-operator
   (state <s> ^name op_put-down1
              ^task-operator.arg1.id <obj>)
-->
   (<s> ^held-object <obj>)
}

sp {op_put-down1*elaborate*predicate-handle*task-operator
   (state <s> ^name op_put-down1
              ^task-operator.arg2.handle <pred-handle>)
-->
   (<s> ^predicate-handle <pred-handle>)
}

sp {op_put-down1*elaborate*destination*task-operator
   (state <s> ^name op_put-down1
              ^task-operator.arg2.{2} <obj>)
-->
   (<s> ^destination <obj>)
}

### CHECK PRECONDITIONS
# If these are violated, quit the current task and re-plan in the parent task

# violated-precondition: the held object is missing
sp {op_put-down1*elaborate*violated-precondition*missing-object*held
   (state <s> ^name op_put-down1
              ^held-object <obj>)
   (<obj> ^predicates.is-confirmed1 not-confirmed1)
-->
   (<s> ^violated-precondition <vp>)
   (<vp> ^type missing-object
         ^object <obj>)
}

#    PL 8/10/2022
# violated-precondition: the arm is not holding anything
# sp {op_put-down1*elaborate*violated-precondition*not-holding-object
#    (state <s> ^name op_put-down1
#               ^top-state.world <world>)
#    (<world> ^robot <rosie>)
#    (<rosie> ^arm.holding-object false)
# -->
#    (<s> ^violated-precondition <vp>)
#    (<vp> ^type not-holding-object)
#    (write |op_put-down1 fails: the arm is not holding anything.|)
# }

# violated-precondition: the destination is missing
sp {op_put-down1*elaborate*violated-precondition*missing-object*destination
   (state <s> ^name op_put-down1
              ^destination <dest>)
   (<dest> ^predicates.is-confirmed1 not-confirmed1)
-->
   (<s> ^violated-precondition <vp>)
   (<vp> ^type missing-object
         ^object <dest>)
   (write |op_put-down1 fails: | <dest> | is not confirmed.|)
}

# violated-precondition: the destination is a closed receptacle
sp {op_put-down1*elaborate*violated-precondition*invalid-predicate*destination*closed
   (state <s> ^name op_put-down1
              ^destination <dest>)
   (<dest> ^predicates <dpreds>)
   (<dpreds> ^affordance1 receptacle1
             ^is-visible1 visible1
             ^is-open1 not-open1)
-->
   (<s> ^violated-precondition <vp>)
   (<vp> ^type invalid-predicate
         ^object <dest>
         ^predicate-handle not-open1)
   (write |op_put-down1 fails: | <dest> | is not open.|)
}
