sp {leave-old-waypoint*elaborate*object-to-store
   (state <s> ^name leave-old-waypoint
              ^top-state.world.objects.object <obj>)
   (<obj> ^svs-obj <svs>)
-->
   (<s> ^object-to-store <obj>
        ^have-objects-to-store true)
}

sp {leave-old-waypoint*propose*extract-metric-info
   (state <s> ^name leave-old-waypoint
              ^have-objects-to-store true
             -^extracted-metric-info true)
-->
   (<s> ^operator <o> +)
   (<o> ^name extract-metric-info)
}

# For each object, extract its metric info from svs
sp {leave-old-waypoint*apply*extract-metric-info*create*filters
   (state <s> ^name leave-old-waypoint
              ^operator.name extract-metric-info
              ^object-to-store.svs-obj.id <id>
              ^top-state.svs.command <cmd>)
-->
   (<cmd> ^extract_once <pos> <rot> <scale>)
   (<pos> ^type node_position
          ^a <node>)
   (<rot> ^type node_rotation
          ^a <node>)
   (<scale> ^type node_scale
            ^a <node>)
   (<node> ^type node
           ^id <id>)
   (<s> ^position-filter <pos>
        ^rotation-filter <rot>
        ^scale-filter <scale>)
}

# Once we get a status from the command mark extracted-metric-info true
sp {leave-old-waypoint*apply*extract-metric-info
   (state <s> ^name leave-old-waypoint
              ^operator.name extract-metric-info
              ^{ <type> << position-filter rotation-filter scale-filter >> } <filter>)
   (<filter> ^status <any>)
-->
   (<s> ^extracted-metric-info true)
}


# Copy the position info onto the object
sp {leave-old-waypoint*apply*extract-metric-info*copy*position
   (state <s> ^name leave-old-waypoint
              ^operator.name extract-metric-info
              ^object-to-store <obj>
              ^position-filter.result.record <r>)
   (<r> ^params.a <id> ^value <pos>)
   (<pos> ^x <x> ^y <y> ^z <z>)
   (<obj> ^svs-obj.id <id>)
-->
   (<obj> ^position <npos>)
   (<npos> ^x <x> ^y <y> ^z <z>)
}

# Copy the rotation info onto the object
sp {leave-old-waypoint*apply*extract-metric-info*copy*rotation
   (state <s> ^name leave-old-waypoint
              ^operator.name extract-metric-info
              ^object-to-store <obj>
              ^rotation-filter.result.record <r>)
   (<r> ^params.a <id> ^value <rot>)
   (<rot> ^x <x> ^y <y> ^z <z>)
   (<obj> ^svs-obj.id <id>)
-->
   (<obj> ^rotation <nrot>)
   (<nrot> ^x <x> ^y <y> ^z <z>)
}

# Copy the scale information onto the object
sp {leave-old-waypoint*apply*extract-metric-info*copy*scale
   (state <s> ^name leave-old-waypoint
              ^operator.name extract-metric-info
              ^object-to-store <obj>
              ^scale-filter.result.record <r>)
   (<r> ^params.a <id> ^value <scale>)
   (<scale> ^x <x> ^y <y> ^z <z>)
   (<obj> ^svs-obj.id <id>)
-->
   (<obj> ^scale <nscale>)
   (<nscale> ^x <x> ^y <y> ^z <z>)
}
   
