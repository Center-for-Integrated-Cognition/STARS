### INSTANTIATE PARTIAL PREDICATE

sp {task-utils*instantiate-task-argument*expand*partial-predicate
   (state <s> ^instantiate-task-argument <arg>)
   (<arg> ^arg-type partial-predicate
          ^general-arg-rep <rep>)
   (<rep> ^id <pred-slot>
          ^2 <obj-slot>)
-->
   (<s> ^instantiate-task-argument <ins-obj> <ins-pred>)

   (<ins-pred> ^arg-type predicate-handle
               ^no-property-handle true
               ^general-arg-rep <pred-rep>)
   (<pred-rep> ^id <pred-slot>)

   (<ins-obj> ^arg-type object
              ^general-arg-rep <obj-rep>)
   (<obj-rep> ^id <obj-slot>)

   (<arg> ^instantiate-handle <ins-pred>
          ^instantiate-obj2 <ins-obj>)
}

sp {task-utils*instantiate-task-argument*propose*instantiate-task-argument*partial-predicate
   (state <s> ^instantiate-task-argument <arg>)
   (<arg> ^arg-type partial-predicate
          ^instantiate-handle.instantiation.handle <handle>
          ^instantiate-obj2.instantiation.id <obj>
         -^instantiation)
-->
   (<s> ^operator <o> + =)
   (<o> ^name instantiate-task-argument
        ^argument <arg>
        ^arg-type predicate
        ^pred-type partial
        ^handle <handle>
        ^2 <obj>)
}

sp {task-utils*instantiate-task-argument*apply*instantiate-task-argument*partial-predicate
   (state <s> ^operator <o>)
   (<o> ^name instantiate-task-argument
        ^argument <arg>
        ^arg-type predicate
        ^pred-type partial
        ^handle <handle>
        ^2 <obj>)
-->
   (<arg> ^instantiation <ins>)
   (<ins> ^handle <handle>
          ^2 <obj>)
}


