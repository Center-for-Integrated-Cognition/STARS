##!
# @file
#
# @created p 20160407
# File: definition.soar
# Author: Peter 
# Created: 7 April 2016
#
# Desc: Define a property
#
#
#       In addition we have added an operator called
#       redefinition to handle the new meaning called
#       PropertyRedefinition.
#       This is needed because the format of the message in the gold
#       standard is quite different than that for a normal definition.
#


#################  PropertyDefinition   ####################### 

#   Propose an operator to define a property word
sp {act*propose*definition
   (state <s> ^problem-space act
              ^action <a>
             -^result.arg1 )    #    Wait until target(s) added
   (<a> ^action DEFINE-PROPERTY
        ^arg1 <word>
        ^property <property>
        ^text <text>)
-->
   (<s> ^operator <o> +)
   (<o> ^name definition
        ^arg1 <word>
        ^property <property>
        ^debug-info (concat |We will create a new property according to '| <text> |.'|))
}

#   Build a message to define a property word
sp {act*apply*definition*create-message
   (state <s> ^problem-space act
              ^operator <o>
              ^action <a>)
   (<o> ^name definition
        ^arg1 <word>
        ^property <property>)
   (<a> ^next <next>)
-->
   #    Create a message
   (<s> ^result <message>)
   (<message> ^message-type adjective-definition
              ^arg1 <word>
              ^property <property>)
   #    On to the next action
   (<s> ^action <a> -
        ^action <next>)
}


#################  PropertyRedefinition   ####################### 

#   This is needed because the format of the message in the gold
#   standard is quite different than that for a normal definition.

#   Propose an operator to redefine a property word
sp {act*propose*redefinition
   (state <s> ^problem-space act
              ^action <a>
             -^result.arg1 )    #    Wait until target(s) added
   (<a> ^action REDEFINE-PROPERTY
        ^arg1 <old-pred>
        ^property <property>
        ^text <text>)
-->
   (<s> ^operator <o> +)
   (<o> ^name redefinition
        ^arg1 <old-pred>
        ^property <property>
        ^debug-info (concat |We will redefine a property according to '| <text> |.'|))
}

#   Build a message to define a property word
sp {act*apply*redefinition*create-message
   (state <s> ^problem-space act
              ^operator <o>
              ^action <a>)
   (<o> ^name redefinition
        ^arg1 <old-pred>
        ^property <property>)
   (<a> ^next <next>)
-->
   #    Create a message
   (<s> ^result <message>)
   (<message> ^message-type adjective-definition
              ^arg1 <old-pred>
              ^property <property>)
   #    On to the next action
   (<s> ^action <a> -
        ^action <next>)
}
