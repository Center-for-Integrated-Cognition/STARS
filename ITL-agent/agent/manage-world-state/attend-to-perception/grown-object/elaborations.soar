sp {attend-to-perception*grown-object*elaborate*object-info
   (state <s> ^name attend-to-perception
              ^discrepancy-type grown-object
              ^discrepancy-info.object-info <obj-info>)
-->
   (<s> ^object-info <obj-info>)
}

sp {attend-to-perception*grown-object*elaborate*agent-params*contained-threshold
   (state <s> ^name attend-to-perception
              ^discrepancy-type grown-object
              ^agent-params.contained-threshold <t>)
-->
   (<s> ^contained-threshold <t>)
}

### We are looking for existing belief objects that the new bbox for the 
#      grown object contains inside

sp {attend-to-perception*grown-object*elaborate*intersecting-non-visible-object
   (state <s> ^name attend-to-perception
              ^discrepancy-type grown-object
              ^intersect-command.result {<belief-id> <> none}
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^belief-id <belief-id>
               ^status.is-visible1 not-visible1)
-->
   (<s> ^intersecting-non-visible-object <belief-id>)
}

sp {attend-to-perception*grown-object*elaborate*overlap-results
   (state <s> ^name attend-to-perception
              ^discrepancy-type grown-object)
-->
   (<s> ^overlap-results <res>)
}

sp {attend-to-perception*grown-object*elaborate*contained-object
   (state <s> ^name attend-to-perception
              ^discrepancy-type grown-object
              ^object-info.perception-obj.id <grown-id>
              ^overlap-results.result <res>
              ^contained-threshold <t>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<res> ^object-a <a>
          ^object-b <grown-id>
          ^overlap > <t>)
   (<obj-info> ^belief-id <a>)
-->
   (<s> ^contained-object-info <obj-info>)
}

sp {attend-to-perception*grown-object*elaborate*found-contained-object
   (state <s> ^name attend-to-perception
              ^discrepancy-type grown-object
              ^contained-object-info <obj>)
-->
   (<s> ^found-contained-object true)
}
