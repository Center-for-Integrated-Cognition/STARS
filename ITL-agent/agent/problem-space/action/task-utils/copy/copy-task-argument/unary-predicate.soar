### COPY UNARY PREDICATE

sp {task-utils*copy-task-argument*expand*unary*predicate
   (state <s> ^copy-task-argument <arg>)
   (<arg> ^arg-type predicate
          ^source-arg <src>
          ^object-map <map>)
   (<src> ^type unary
          ^1 <obj1>)
-->
   (<arg> ^obj1-copy <obj1-copy>)

   (<s> ^copy-task-argument <obj1-copy>)
   (<obj1-copy> ^arg-type object
                ^source-arg.id <obj1>
                ^object-map <map>)
}

sp {task-utils*copy-task-argument*propose*copy-task-argument*unary*predicate
   (state <s> ^copy-task-argument <arg>)
   (<arg> ^arg-type predicate
          ^source-arg <src>
          ^obj1-copy.arg-copy.id <obj1>
         -^arg-copy)
   (<src> ^type unary
          ^handle <h>
          ^property-handle <prop-h>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-task-argument
        ^argument <arg>
        ^arg-type predicate
        ^pred-type unary
        ^pred-handle <h>
        ^property-handle <prop-h>
        ^obj1 <obj1>)
}

sp {task-utils*copy-task-argument*apply*copy-task-argument*unary*predicate
   (state <s> ^operator <o>)
   (<o> ^name copy-task-argument
        ^argument <arg>
        ^arg-type predicate
        ^pred-type unary
        ^pred-handle <h>
        ^property-handle <prop-h>
        ^obj1 <obj1>)
-->
   (<arg> ^arg-copy <copy>)
   (<copy> ^type unary
           ^handle <h>
           ^property-handle <prop-h>
           ^1 <obj1>)
}


