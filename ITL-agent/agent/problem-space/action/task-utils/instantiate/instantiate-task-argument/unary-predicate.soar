### INSTANTIATE UNARY PREDICATE

sp {task-utils*instantiate-task-argument*expand*unary*predicate
  (state <s> ^instantiate-task-argument <arg>)
  (<arg> ^arg-type predicate
         ^general-arg-rep <rep>)
  (<rep> ^type unary
         ^id <pred-slot>
         ^1 <obj1-slot>)
-->
  (<s> ^instantiate-task-argument <ins-obj1> <ins-pred>)

  (<ins-pred> ^arg-type predicate-handle
              ^general-arg-rep <gen-pred>)
  (<gen-pred> ^id <pred-slot>)

  (<ins-obj1> ^arg-type object
              ^general-arg-rep <gen-obj1>)
  (<gen-obj1> ^id <obj1-slot>)

  (<arg> ^instantiate-handle <ins-pred>
         ^instantiate-obj1 <ins-obj1>)
}

sp {task-utils*instantiate-task-argument*propose*instantiate-task-argument*predicate*unary
  (state <s> ^instantiate-task-argument <arg>)
  (<arg> ^arg-type predicate
         ^general-arg-rep.type unary
         ^instantiate-handle.instantiation <i>
         ^instantiate-obj1.instantiation.id <obj1>
        -^instantiation)
   (<i> ^handle <handle>
        ^property-handle <prop-handle>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type unary
       ^handle <handle>
       ^property-handle <prop-handle>
       ^1 <obj1>)
}

sp {task-utils*instantiate-task-argument*apply*instantiate-task-argument*predicate*unary
  (state <s> ^operator <o>)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type unary
       ^handle <handle>
       ^property-handle <prop-handle>
       ^1 <obj1>)
-->
  (<arg> ^instantiation <ins>)
  (<ins> ^type unary
         ^handle <handle>
         ^property-handle <prop-handle>
         ^1 <obj1>)
}


