#pretty hacky method make more general
sp {convert-NLP-to-language*elaborate*relationships
   (state <s> ^name convert-NLP-to-language
              ^superstate.operator.structure <ps>)
   -->
   (<s> ^relationships <gr>
        ^functions <fc>
        ^structure <ps>)
}
sp {convert-NLP-to-language*elaborate*conditions
   (state <s> ^name convert-NLP-to-language
              ^structure.conditions <cond>)
-->
    (<s> ^condition <cond>)
  }
sp {convert-NLP-to-language*elaborate*conditions*depth
   (state <s> ^name convert-NLP-to-language
              ^condition <cond>)
   (<cond> ^args.<< 1 2 3 4 5 6 >> <cond2>)
-->
    (<s> ^condition <cond2>)
  }

sp {convert-NLP-to-language*elaborate*object-description
   (state <s> ^name convert-NLP-to-language
              ^condition <cond>)
   (<cond> ^name <name> -^attribute input-arg  -^attribute primitive ^attribute <at> ^type attribute)
-->
    (<s> ^<cond> <desc1>)
    (<desc1> ^<at> <name>)
}

sp {convert-NLP-to-language*elaborate*object-description*function
   (state <s> ^name convert-NLP-to-language
              ^<c> <desc1>
              ^condition <cond>)
   (<cond> ^name <name> ^type attribute-of ^args.{2} <c> ^parameter.{1}.param-id <num>)
-->
    (<s> ^<num> <f>)
    (<f> ^type <name> ^of <desc1>)
}

sp {convert-NLP-to-language*elaborate*object-description*primitive
   (state <s> ^name convert-NLP-to-language
              ^condition <cond>)
   (<cond> ^name <name>   ^attribute primitive ^type attribute)
-->
    (<s> ^<cond> <desc1>)
    (<desc1> ^type <name>)
}

sp {convert-NLP-to-language*elaborate*object-description*input-arg
   (state <s> ^name convert-NLP-to-language
              ^condition <cond>)
   (<cond> ^name object ^attribute input-arg )
-->
    (<s> ^<cond> <desc1>)
    (<desc1> ^type it)
}

sp {convert-NLP-to-language*elaborate*object-description*telescope
   (state <s> ^name convert-NLP-to-language
              ^condition <cond>
              ^<c2> <desc2>
              ^<cond> <desc>)
   (<cond> ^args.{1} <c2>)
   (<desc2> ^<something> <value>)
-->
    (<desc> ^<something> <value>)
}

sp {convert-NLP-to-language*apply*state-diff*copy-goal-relations
   (state <s> ^name convert-NLP-to-language
              ^relationships <gr>
              ^condition <c>)
   (<s> ^<c1> <desc1> ^<c2> <desc2>)
   (<c> ^args <a> ^name <name> ^negative <n>)
   (<a> ^1 <c1> ^2 <c2> -^3)
-->
   (<gr> ^relation <r1>)
   (<r1> ^1 <desc1> ^2 <desc2> ^name <name> ^negative <n>)
}

sp {convert-NLP-to-language*apply*state-diff*copy-function-relations
   (state <s> ^name convert-NLP-to-language
              ^functions <gr>
              ^<num1> <f1>
              ^<num1> {<f2> <> <f1>})
   (<f1> ^type <name1> ^of <desc1>)
   (<f2> ^type <name2> ^of <desc2>)
   (<desc2> ^type it)#need bias
-->
   (<gr> ^function <r1>)
   (<r1> ^1 <desc1> ^2 <desc2> ^of1 <name1> ^of2 <name2> ^type is)
}

sp {convert-NLP-to-language*propose*complete
   (state <s> ^name convert-NLP-to-language)
-->
   (<s> ^operator <o> + <)
   (<o> ^name complete)
}

sp {convert-NLP-to-language*apply*complete
   (state <s> ^name convert-NLP-to-language
              ^relationships <gr>
              ^functions <fc>
              ^superstate <ss>
              ^operator.name complete)
-->
    (<ss> ^nlp-relationships <gr> ^nlp-functions <fc>)
}