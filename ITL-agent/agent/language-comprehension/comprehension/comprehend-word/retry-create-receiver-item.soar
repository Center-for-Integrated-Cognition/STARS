sp {comprehend-word*propose*retry-create-receiver
   (state <s> ^name << comprehend-word comprehend-construction >>
              ^segment <seg>
              ^current-word <cw>
              ^retrieved-lexical-item <id>)
   (<seg> ^retry <retry>)
  -{(<s> ^comprehension-structure.not-merged-receiver <uri>)
    (<uri> ^lt <id>
           ^current-word <cw>)}
   -(<retry> ^lt <id>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name retry-create-receiver
         ^retry <retry>)
}
sp {comprehend-word*compare*retry-create-receiver
   (state <s> ^name << comprehend-word comprehend-construction >>
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name retry-create-receiver)
   (<op2> ^name << retrieve-semantic-substructure remove-stale-receiver retrieve-semantic-substructure >>)
-->
   (<s> ^operator <op1> < <op2>)
}

sp {apply*retry-create-receiver*create-item
#:interrupt
   (state <s> ^operator <op>
              ^current-word <cw>
              ^retrieved-lexical-item <id>
              ^segment <seg>
             -^name comprehend-construction
             ^comprehension-structure <cs>)
   (<seg> ^retry <retry>)
   (<op> ^name retry-create-receiver
         ^retry <retry>)
   (<id> ^structure-type <stype>
        -^constraint)
   (<retry> ^structure-type { <> <stype> <old-type> } 
            ^lt <old-id>)
-->
   (<seg> ^retry <retry> -
          ^parse-failed-end-of-sentence true -
          ^retried <retry>)
   (<cw> ^processed true)
   (<retry> ^lt <id>
                <old-id> -
            ^structure-type <stype>
                            <old-type> -)}

sp {apply*retry-create-receiver*create-item*parse-failed-receiver
#:interrupt
   (state <s> ^operator <op>
              ^segment <seg>)
   (<seg> ^parse-failed-receiver <pfr>)
   (<op> ^name retry-create-receiver)
   -->
   (<seg> ^parse-failed-receiver <pfr> -
          ^parse-failed-end-of-sentence true -)}

sp {apply*retry-create-receiver*create-item*delete*retrieved-lexical-item
#:interrupt
   (state <s> ^operator <op>
              ^current-word <cw>
              ^retrieved-lexical-item <id>
              ^segment <seg>
             -^name comprehend-construction
              ^comprehension-structure <cs>)
   (<seg> ^retry <retry>)
   (<op> ^name retry-create-receiver
         ^retry <retry>)
   (<id> ^structure-type <stype>
        -^constraint)
   (<retry> ^structure-type <stype>  
            ^lt <old-id>)
-->
   #(write (crlf) |Delete lexical item mistakenly retrieved: | <id>)
   (<s> ^retrieved-lexical-item <id> -)}

