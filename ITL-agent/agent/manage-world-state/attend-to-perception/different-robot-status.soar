# different-robot-status
#   We simply update the moving-status on the robot/arm

### moving-status on the robot

sp {anystate*apply*attend-to-perception*different-robot-status*add*new*status
   (state <s> ^operator <o>
              ^top-state.world.robot <world-robot>)
   (<o> ^name attend-to-perception
        ^discrepancy-type different-robot-status
        ^discrepancy-info.robot-status <status>)
-->
   (<world-robot> ^moving-status <status>)
}

sp {anystate*apply*attend-to-perception*different-robot-status*remove*old*status
   (state <s> ^operator <o>
              ^top-state.world.robot <world-robot>)
   (<o> ^name attend-to-perception
        ^discrepancy-type different-robot-status
        ^discrepancy-info.robot-status <status>)
   (<world-robot> ^moving-status { <old-status> <> <status> })
-->
   (<world-robot> ^moving-status <old-status> -)
}

# if print-perception is on, print a message
sp {anystate*apply*attend-to-perception*different-robot-status*print-perception
   (state <s> ^operator <o>
              ^agent-params.print-perception true)
   (<o> ^name attend-to-perception
        ^discrepancy-type different-robot-status
        ^discrepancy-info.robot-status <status>)
-->
   (write |attend-to-perception(different-robot-status): Robot status is now | <status> (crlf))
}

### moving-status on the arm

sp {anystate*apply*attend-to-perception*different-robot-status*arm*add*new*status
   (state <s> ^operator <o>
              ^top-state.world.robot.arm <world-arm>)
   (<o> ^name attend-to-perception
        ^discrepancy-type different-robot-status
        ^discrepancy-info.arm-status <status>)
-->
   (<world-arm> ^moving-status <status>)
}

sp {anystate*apply*attend-to-perception*different-robot-status*arm*remove*old*status
   (state <s> ^operator <o>
              ^top-state.world.robot.arm <world-arm>)
   (<o> ^name attend-to-perception
        ^discrepancy-type different-robot-status
        ^discrepancy-info.arm-status <status>)
   (<world-arm> ^moving-status { <old-status> <> <status> })
-->
   (<world-arm> ^moving-status <old-status> -)
}

# if print-perception is on, print a message
sp {anystate*apply*attend-to-perception*different-robot-status*arm*print-perception
   (state <s> ^operator <o>
              ^agent-params.print-perception true)
   (<o> ^name attend-to-perception
        ^discrepancy-type different-robot-status
        ^discrepancy-info.arm-status <status>)
-->
   (write |attend-to-perception(different-robot-status): Robot arm status is now | <status> (crlf))
}
