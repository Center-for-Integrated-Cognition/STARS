###################################################################
#
# interpret-predicate-question
#   Occurs when the instructor wishes to get details about the properties of an object
#   Construction: *-What-DP-T is the object PP?  
#   	E.g. 'What shape is the object on the stove?' 
#
###################################################################

#############
# OPERATOR PROPOSAL
#############
sp {interpret-message*propose*interpret-predicate-question
   (state <s> ^name interpret-message
              ^instructor-message <msg>)
   (<msg> ^message-type predicate-question
          -^interpreted true)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-predicate-question
        ^message <msg>)
}

######
# OPERATOR ELABORATIONS
######

# Elaborate useful information from the message onto the operator
# message.arg1 is the object whose details are being inquired
sp {interpret-message*elaborate*interpret-predicate-question*message
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-predicate-question
              ^message.property <prop>
              ^message.arg1 <obj>)
-->
   (<o> ^property <prop>
        ^object <obj>)
}

#####
# OPERATOR APPLICATION
#####

sp {interpret-message*apply*interpret-predicate-question*create*message
   (state <s> ^name interpret-message
              ^instructor-message <msg>
              ^operator <o>)
   (<o> ^name interpret-predicate-question
        ^property <prop>
        ^object <obj>)
-->
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type agent-predicate-description
   		^parameters <params>
        ^satisfaction <sat>)
   (<params> ^object <obj>
             ^property <prop>)
   (<sat> ^dialog-event <de>)
   (<de> ^type explanation
         ^originator agent)
}
  