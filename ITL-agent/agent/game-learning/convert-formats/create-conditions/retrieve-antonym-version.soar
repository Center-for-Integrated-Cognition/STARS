sp {retrieve-antonym-version*elaborate*handle-spelling
   (state <s> ^name retrieve-antonym-version
              ^superstate.operator <sop>)
   (<sop> ^handle <handle>)
-->
   (<s> ^handle-spelling <handle>)
}
#hmm dont want to build antonym with empty version...
sp {retrieve-antonym-version*elaborate*default-version
   (state <s> ^name retrieve-antonym-version
              ^superstate.operator <sop>)
   (<sop> ^default-version true)
-->
   (<s> ^default-version true)
}
sp {retrieve-antonym-version*elaborate*smem-query
   (state <s> ^name retrieve-antonym-version
              ^smem.command <c>
			 -^default-version true
			  ^handle-spelling <handle>)
-->	
   (<c> ^query <cue>
        ^neg-query <ncue> 
        ^depth 5)
   (<cue> ^handle <x> ^handle-spelling <handle>)
   (<ncue> ^multiple true)
}
#TODO but dont retrieve empty language concepts as antonyms..
sp {retrieve-antonym-version*elaborate*smem-query*default
   (state <s> ^name retrieve-antonym-version
              ^smem.command <c>
			  ^default-version true
			  ^handle-spelling <handle>)
-->	
   (<c> ^query <cue>
        ^neg-query <ncue> #TODO add neg cue for handle-spelling
        ^depth 5)
   (<cue> ^handle <handle>)
   (<ncue> ^multiple true)
}
sp {retrieve-antonym-version*elaborate*smem-query*prohibit
   (state <s> ^name retrieve-antonym-version
              ^smem.command <com>
              ^superstate.antonym-retrieved <pr>)
-->	
   (<com> ^prohibit <pr>)
}
sp {retrieve-antonym-version*elaborate*smem-query*failure
:o-support
   (state <s> ^name retrieve-antonym-version
   		  	  ^superstate <ss>
			  ^handle-spelling <ch>
              ^smem.result.failure <rr>)
-->
   (<ss> ^completed-antonym <ch>)
}
sp {retrieve-antonym-version*elaborate*smem-query*antonym-retrieved
:o-support
   (state <s> ^name retrieve-antonym-version
   		  	  ^superstate <ss>
              ^smem.result.retrieved <rr>)
   (<rr> ^handle <cn>)
-->
   (<ss> ^antonym-retrieved <rr>)
}
sp {retrieve-antonym-version*elaborate*compeleted-true
:o-support
   (state <s> ^name retrieve-antonym-version
   		  	  ^superstate <ss>
              ^smem.result.retrieved <rr>)
   (<rr> ^handle <cn>)
   (<ss> ^operator <o>)
-->
   (<o> ^completed true)
}
sp {retrieve-antonym-version*elaborate*compeleted-true*failure
:o-support
   (state <s> ^name retrieve-antonym-version
   		  	  ^superstate <ss>
              ^smem.result.failure <rr>)
   (<ss> ^operator <o>)
-->
   (<o> ^completed true)
}

#######################
####     DEBUG     ####
#######################
sp {retrieve-antonym-versions*elaborate*smem-query*prohibitPRINT
   (state <s> ^name retrieve-antonym-versionsXX
              ^smem.command <c>)
   (<c> ^query <q>)
#(<q> ^handle-spelling empty) 
-->	
   (interrupt)
   (write (cmd print <c> -d 3))
}