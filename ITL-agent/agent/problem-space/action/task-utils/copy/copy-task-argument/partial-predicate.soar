### COPY PARTIAL-PREDICATE ARGUMENTS

sp {task-utils*copy-task-argument*expand*partial-predicate*argument*copy*obj2
   (state <s> ^copy-task-argument <arg>)
   (<arg> ^arg-type partial-predicate
          ^source-arg <src>
          ^object-map <map>)
   (<src> ^handle <h>
          ^2 <obj2>)
-->
   (<arg> ^obj2-copy <obj2-copy>
          ^pred-handle <h>)
   (<s> ^copy-task-argument <obj2-copy>)
   (<obj2-copy> ^arg-type object
                ^source-arg.id <obj2>
                ^object-map <map>)
}

sp {task-utils*copy-task-argument*propose*copy-task-argument*partial-predicate
   (state <s> ^copy-task-argument <arg>)
   (<arg> ^arg-type partial-predicate
          ^pred-handle <h>
          ^obj2-copy.arg-copy.id <obj2-copy>
         -^arg-copy)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-task-argument
        ^argument <arg>
        ^arg-type partial-predicate
        ^pred-handle <h>
        ^obj2 <obj2-copy>)
}

sp {task-utils*copy-task-argument*apply*copy-task-argument*partial-predicate
   (state <s> ^operator <o>)
   (<o> ^name copy-task-argument
        ^argument <arg>
        ^arg-type partial-predicate
        ^pred-handle <h>
        ^obj2 <obj2>)
-->
   (<arg> ^arg-copy <copy>)
   (<copy> ^handle <h>
           ^2 <obj2>)
}
