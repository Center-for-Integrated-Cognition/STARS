### INSTANTIATE RELATION PREDICATE

sp {task-utils*instantiate-task-argument*expand*relation*predicate
  (state <s> ^instantiate-task-argument <arg>)
  (<arg> ^arg-type predicate
         ^general-arg-rep <rep>)
  (<rep> ^type relation
         ^id <pred-slot>
         ^1 <obj1-slot>
         ^2 <obj2-slot>)
-->
  (<s> ^instantiate-task-argument <ins-pred> <ins-obj1> <ins-obj2>)

  (<ins-pred> ^arg-type predicate-handle
              ^no-property-handle true
              ^general-arg-rep <pred-rep>)
  (<pred-rep> ^id <pred-slot>)

  (<ins-obj1> ^arg-type object
              ^general-arg-rep <obj1-rep>)
  (<obj1-rep> ^id <obj1-slot>)

  (<ins-obj2> ^arg-type object
              ^general-arg-rep <obj2-rep>)
  (<obj2-rep> ^id <obj2-slot>)

  (<arg> ^instantiate-handle <ins-pred>
         ^instantiate-obj1 <ins-obj1>
         ^instantiate-obj2 <ins-obj2>)
}

sp {task-utils*instantiate-task-argument*propose*instantiate-task-argument*predicate*relation
  (state <s> ^instantiate-task-argument <arg>)
  (<arg> ^arg-type predicate
         ^general-arg-rep.type relation
         ^instantiate-handle.instantiation.handle <handle>
         ^instantiate-obj1.instantiation.id <obj1>
         ^instantiate-obj2.instantiation.id <obj2>
         -^instantiation)
-->
  (<s> ^operator <o> + =)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type relation
       ^handle <handle>
       ^1 <obj1>
       ^2 <obj2>)
}

sp {task-utils*instantiate-task-argument*apply*instantiate-task-argument*predicate*relation
  (state <s> ^operator <o>)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type relation
       ^handle <handle>
       ^1 <obj1>
       ^2 <obj2>)
-->
  (<arg> ^instantiation <ins>)
  (<ins> ^type relation
         ^handle <handle>
         ^1 <obj1>
         ^2 <obj2>)
}


