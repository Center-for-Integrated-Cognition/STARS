sp {send-message*get-item-request*elaborate*object-description*item
   (state <s> ^name send-message
              ^message.type get-item-request
              ^message.item <obj>)
-->
   (<s> ^object-description <d>)
   (<d> ^object <obj>)
}

sp {send-message*propose*create-get-item-request
   (state <s> ^name send-message
              ^message.type get-item-request
             -^created-message)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-get-item-request)
}

sp {send-message*apply*create-get-item-request*create*message
   (state <s> ^name send-message
              ^operator.name create-get-item-request
              ^message.item <obj>
              ^object-description <d1>)
   (<d1> ^object <obj>
         ^translation <t1>)
-->
   (<s> ^created-message <msg>)
   (<msg> ^type get-item-request
          ^fields <f>)
   (<f> ^item <t1>)
}
