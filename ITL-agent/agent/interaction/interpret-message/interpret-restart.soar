###################################################################
#
# interpret-restart
#   Occurs when the intructor wants to start the interaction over
#   Construction: *-restart-T
#
###################################################################

#######
# OPERATOR PROPOSAL
#######
sp {interpret-message*propose*interpret-restart
   (state <s> ^name interpret-message
              ^instructor-message <msg>)
   (<msg> ^message-type restart
         -^interpreted true)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-restart
        ^message <msg>)
}

#####
# OPERATOR APPLICATION
#####

# Don't even need to do any special processing, just create a response
sp {interpret-message*apply*interpret-restart*mark
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-restart
        ^message <msg>)
-->
   (<msg> ^interpreted true)
}

sp {interpret-message*apply*interpret-restart*top-state*mark*reset-agent-state*true
   (state <s> ^name interpret-message
              ^operator.name interpret-restart
              ^top-state <ts>)
-->
   (<ts> ^reset-agent-state true)
}
