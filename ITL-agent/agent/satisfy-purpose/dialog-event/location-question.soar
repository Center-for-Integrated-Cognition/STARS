sp {dialog-event*propose*describe-object-location
   (state <s> ^name dialog-event
              ^purpose <p>)
   (<p> ^type location-question
        ^parameters.object <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name describe-object-location
        ^object <obj>)
}

sp {dialog-event*location-question*apply*describe-object-location
   (state <s> ^name dialog-event
              ^operator <o>
              ^top-state.world.predicates <wpreds>)
   (<o> ^name describe-object-location
        ^object <obj>)
   (<wpreds> ^predicate <pred>)
   (<pred> ^instance <instance>
           ^handle { <handle> << in1 on1 >> })
   (<instance> ^1 <obj>
               ^2 <loc>)
-->
   (<s> ^description <desc>)
   (<desc> ^location <loc>
           ^handle <handle>)
}

#######################################################
# Report answer using complete-describe-object-location
#######################################################

sp {describe-object-location*location-question*propose*complete-describe-object-location
   (state <s> ^name describe-object-location
              ^superstate <ss>)
   (<ss> ^description <desc>)
-->
   (<s> ^operator <o> + <)
   (<o> ^name complete-describe-object-location
        ^description <desc>)
}
 
sp {describe-object-location*location-question*propose*complete-describe-object-location*mark*performed
   (state <s> ^name describe-object-location
              ^operator.name complete-describe-object-location
              ^top-state.interaction.status <status>)
-->
   (<status> ^performed.dialog-event <de>)
   (<de> ^type explanation
         ^originator agent)
}  

sp {describe-object-location*location-question*propose*complete-describe-object-location*generated*send-message
   (state <s> ^name describe-object-location
              ^operator <o>  
              ^io.output-link <out>)
   (<o> ^name complete-describe-object-location
        ^description <desc>)
-->
   (<out> ^send-message <msg>)
   (<msg> ^type agent-location-description
          ^fields.location-description <desc>)
}        
        
        
   